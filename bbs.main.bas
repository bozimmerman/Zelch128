!--------------------------------------------------
!- Monday, May 15, 2017 12:15:29 AM
!- Import of : 
!- c:\src\zelch128\bbs.main.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
1 A$="":O$="":I$="":C$="":TRAP360:R$=CHR$(13):SYS15622:U=PEEK(186):DLOAD"bbs.setup",U(U),D0
2 IFM(0)>2THENRETURN:ELSESLEEP1:FAST:I=0:O$="":PRINT#5,"at"+A$:DO:GET#5,O$:IFINSTR("0{cm a}",O$)THENGET#5,O$:IFO$=R$THENEXIT
3 I=I+1:LOOPUNTILI=200:IFI=200THENPRINT#5,R$R$:GOTO2:ELSEPOKE56577,M(5):POKE56579,M(6):IFPEEK(215)=0THENSLOW
4 IFO$=""THENO$=R$:GOTO9:ELSEIFINSTR(O$,"@")THEN32:ELSESYS8204:IFPEEK(2896)=2THENPOKE2896,0:GOSUB300:RETURN:ELSERETURN
5 POKE2847,X:POKE2895,1:POKE2831,1
6 SYS8201:ON(PEEK(2827)=1)+2GOTO600:RETURN
7 IFMID$(U$(8),1,1)="1"ANDP0<101THEN11
8 SYS8192:ON(PEEK(2827)=1)+2GOTO600:IFPEEK(211)=8THENW1=PEEK(2823):GOTO751:ELSERETURN
9 POKE254,ASC(O$+CHR$(0)):SYS8198:RETURN
10 I=0:GOSUB6:IFI$=""ORLEN(I$)>4ORINSTR(I$,"e")THENRETURN:ELSEI=VAL(I$):RETURN
11 A$=RIGHT$(O$,1):IFA$=CHR$(13)THENA$="+":MID$(O$,LEN(O$),1)=A$
12 IFLEFT$(O$,3)=LEFT$(P0$(P0),3)ANDLEN(P0$(P0))<150THENP0$(P0)=P0$(P0)+","+A$:RETURN:ELSEIFP0$(P0)<>""THENP0=P0+1:P0$(P0)=O$:RETURN:ELSEP0$(P0)=O$:RETURN
13 POKE56577,M(5):POKE56579,M(6):IFM(0)>2OR(PEEK(56577)ANDM(4))=PEEK(2853)THENRETURN
14 SLEEP2:FORX=1TO3:PRINT#5,"+";:FORI=1TO200:NEXTI:NEXTX:SLEEP2:PRINT#5,R$+"ath0":FORX=1TO100:GET#5,A$:NEXTX:IF(PEEK(56577)ANDM(4))=PEEK(2853)THENRETURN:ELSE14
18 IFM(0)<3THENPRINT#5,R$:SLEEP1:PRINT#5,"ats0=0":RETURN:ELSEPOKE56577,M(7):POKE56579,M(8):RETURN
23 IFO$(11)=I$THEN30010:ELSEO$(11)=I$:I=LEN(I$)-4:O$="{reverse on}{light gray}"+RIGHT$(I$,I)+LEFT$("          ",12-I):X=PEEK(238)-12:Y=24:GOSUB30:SYS15622:DLOAD(I$),U(U),D(D)
25 O$=T$(1):GOSUB4
26 OPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,r":SYS8210:INPUT#1,I:CLOSE1:CLOSE2:C$="":IFPEEK(252)=0THENRETURN
27 POKE2925,0:O$="{ct k}return>{ct c}":GOSUB4:POKE2925,M(3):C$=R$:GOSUB28:O$=A$+"{ct n}":GOSUB4:RETURN
28 A$="":DOUNTILINSTR(C$,A$)>0:POKE2831,1:GOSUB8:LOOP:I=INSTR(C$,A$):RETURN
29 PRINT#5,R$R$R$R$:SLEEP1:PRINT#5,"atz":SLEEP1:FORI=0TO100:GET#5,I$:NEXT:RETURN
30 POKE2861,X:POKE2862,Y:SYS15631:RETURN
31 SYS8222:POKE252,0:POKE253,0:IFI$=CHR$(1)THENPOKE252,1:RETURN:ELSEIFST>0THENPOKE253,1:PRINT#3,I$:RETURN:ELSEPRINT#3,I$:GOTO31
32 U6=INSTR(O$,"@"):A=VAL(MID$(O$,U6+2,1)):ONINSTR("untw",MID$(O$,U6+1,1))GOSUB34,35,36,38:O$=LEFT$(O$,U6-1)+A$+MID$(O$,U6+3):GOTO4
34 IFA=0THENA$=MID$(STR$(ID),2):RETURN:ELSEA$=U$(A):RETURN
35 A$=MID$(STR$(U(A)),2):RETURN
36 IFA=0THENSYS8216:A$=I$:RETURN:ELSEIFA<5THENGOSUB400:A$=O$(A):RETURN:ELSEIFA=5THENA$=M6$:RETURN:ELSEIFA=6THENA$=M7$:RETURN
37 IFA=7THENA$=MID$(STR$(PEEK(2825)),2):RETURN:ELSEA$=O$(12):RETURN
38 DO:GOSUB8:LOOPUNTILA$<>"":A$="":RETURN
40 OPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,r":INPUT#1,X
41 SYS8222:IFPEEK(253)ORI$=CHR$(1)THENCLOSE2:CLOSE1:O$=I$:GOSUB4:I=FRE(1):RETURN:ELSEO$=I$:GOSUB4:GOTO41
55 OPEN1,8,15,"s0:bbs.main":CLOSE1:DSAVE"bbs.main",U8:STOP
99 REM --------------- UPDATE ALL WINDOW DATA ---------------------------------
100 I$="                ":IFPEEK(215)THEN200:ELSEX=7:Y=0:O$="{light gray}{reverse on}"+LEFT$(U$(1)+I$,16):GOSUB30:Y=1:O$="{reverse on}"+LEFT$(U$(3)+I$,16):GOSUB30:Y=2:O$="{reverse on}"+U$(4):GOSUB30
105 Y=3:O$="{reverse on}"+U$(5):GOSUB30:Y=0:X=27:O$="{reverse on}"+LEFT$(MID$(STR$(ID),2,3)+I$,3):GOSUB30:X=33:O$="{reverse on}"+RIGHT$(I$+MID$(STR$(BA),2,4),4):GOSUB30:X=30:Y=1
110 O$="{reverse on}"+RIGHT$("00"+MID$(U$(9),2,2),2)+"/"+RIGHT$("00"+MID$(STR$(CD(ASC(U$(5))-192,0)),2,2),2):GOSUB30:Y=2:O$="{reverse on}"+LEFT$(O$(1),7):GOSUB30:X=11:Y=3
120 O$="":FORI=1TO26:IFMID$(U$(7),I,1)="1"THENO$=O$+"{reverse off}":ELSEO$=O$+"{reverse on}"
125 O$=O$+CHR$(I+192):NEXTI:GOSUB30:GOTO300
200 X=16:Y=0:O$="{light gray}{reverse on}"+LEFT$(U$(1)+I$,16):GOSUB30:Y=1:O$="{reverse on}"+LEFT$(U$(3)+I$,16):GOSUB30:Y=2:O$="{reverse on}"+U$(4):GOSUB30:Y=3:O$="{reverse on}"+O$(1):GOSUB30:X=38:Y=0
210 O$="{reverse on}"+LEFT$(MID$(STR$(ID),2,3)+I$,3):GOSUB30:Y=1:O$="{reverse on}"+LEFT$(O$(4),8):GOSUB30:Y=2:O$="{reverse on}"+LEFT$(O$(3),8):GOSUB30:X=57:Y=0:O$="{reverse on}"+U$(5):GOSUB30
220 Y=1:O$="{reverse on}"+LEFT$(MID$(STR$(U(1)),2,5)+I$,5):GOSUB30:Y=2:O$="{reverse on}"+LEFT$(MID$(STR$(U(2)),2,5)+I$,5):GOSUB30:X=70:Y=1
225 O$="{reverse on}"+RIGHT$("00"+MID$(U$(9),2,2),2)+"/"+RIGHT$("00"+MID$(STR$(CD(ASC(U$(5))-192,0)),2,2),2):GOSUB30:Y=0:O$="{reverse on}"+LEFT$(MID$(STR$(BA),2,4)+I$,4):GOSUB30
230 Y=2:O$="{reverse on}"+MID$(STR$(40+(40*VAL(MID$(U$(8),2,1)))),2,2):GOSUB30:Y=3:X=23:O$="{ct b}":FORI=1TO26:O$=O$+"{reverse on} ":IFMID$(U$(7),I,1)="1"THENO$=O$+"{reverse off}"
240 O$=O$+CHR$(I+192):NEXTI:O$=O$+"{130}":GOSUB30:GOTO300
249 REM ------------ BOTTOM WINDOW UPDATE ------------
250 X=0:Y=24:O$="{reverse on}{light gray}"+LEFT$(O$+"                                              {sh space}           ",PEEK(238)-20):GOSUB30:RETURN
299 REM --------------------- UPDATE CHAT AND GRFX FLAGS ----------------------
300 X=0:Y=0:O$="{light gray}":IFPEEK(2896)THENO$="{pink}{ct o}"
305 IFPEEK(215)THEN350:ELSEO$=O$+"{reverse on}C{down}{left}H{down}{left}A{down}{left}T{light gray}":GOSUB30:X=38:O$="{light gray}":IFPEEK(2829)THENO$="{gray}"
310 O$=O$+"{reverse on}G{down}{left}R{down}{left}F{down}{left}X{light gray}":GOSUB30:RETURN
350 O$=O$+"{reverse on} C{cm m}{down}{left*3} H{cm m}{down}{left*3} A{cm m}{down}{left*3} T{cm m}{down}{left*3}{143}{light gray}":GOSUB30:X=76:O$="{light gray}":IFPEEK(2829)THENO$="{dark gray}"
355 O$=O$+"{reverse on}{cm g}G {down}{left*3}{cm g}R {down}{left*3}{cm g}F {down}{left*3}{cm g}X {light gray}":GOSUB30:RETURN
360 GOSUB400:I$="["+ERR$(ER)+"] line"+STR$(EL):O$="{ct k}{white}"+I$:GOSUB4:GOSUB7:IFER=30THENEND
365 O$="Standby, recording...{ct c}":GOSUB4:O(5)=O(5)+1:CLOSE2:CLOSE1:OPEN1,D(1,0),15:OPEN2,D(1,0),2,STR$(D(1,1))+":sys.errorlog,s,a":INPUT#1,E:IFE=0THEN375
370 CLOSE2:OPEN2,D(1,0),2,STR$(D(1,1))+":sys.errorlog,s,w"
375 PRINT#2,O$(3)+", "+I$+", "+O$(11)+", "+O$(15):CLOSE2:CLOSE1:CLOSE3:CLOSE4:TRAP360:IFID=0THEN600:ELSE920
399 REM --------------------- CREATION OF O$(1-4) --------------------------
400 SYS8216:A$=RIGHT$(STR$(PEEK(2863)),2)+RIGHT$(STR$(PEEK(2879)),2)+RIGHT$(STR$(PEEK(2880)),2)+LEFT$(I$,2)+MID$(I$,4,2):IFLEFT$(I$,2)="12"THENMID$(A$,7,2)="00"
405 DOUNTILINSTR(A$," ")=0:MID$(A$,INSTR(A$," "),1)="0":LOOP:IFRIGHT$(O$(1),2)="pm"THENMID$(A$,7,2)=RIGHT$(STR$(VAL(MID$(A$,7,2))+12),2)
410 O$(2)=A$:SYS8216:O$(3)=MID$(O$(2),3,2)+"/"+MID$(O$(2),5,2)+"/"+LEFT$(O$(2),2)+"  "+I$
415 O$(4)=MID$(U$(6),3,2)+"/"+MID$(U$(6),5,2)+"/"+LEFT$(U$(6),2)+"  "+MID$(U$(6),7,2)+":"+MID$(U$(6),9,2)+"am"
420 IFMID$(U$(6),7,2)>"12"THENMID$(O$(4),11,2)=RIGHT$(STR$(VAL(MID$(U$(6),7,2))-12),2):MID$(O$(4),16,2)="pm":IFINSTR(O$(4)," ",11)>1THENMID$(O$(4),11,1)="0"
430 RETURN
449 REM ------------------------ BAUD CHANGE -----------------------------
450 POKE2576,6:BA=I:IFI=1200THENPOKE2576,8:ELSEIFI=2400THENPOKE2576,10
460 A=1022730/BA:GOSUB480:POKE15104,X:POKE15105,Y:IFBA>1199THENA=1022730/BA*.996:GOSUB480
470 POKE2582,X:POKE2583,Y:A=1022730/BA/2-100:GOSUB480:POKE2578,X:POKE2579,Y:RETURN
480 Y=INT(A/256):X=A-(Y*256):RETURN
499 REM---------------------- TRANSFER ---------------------------------
500 E=0:GOSUB590:SYS12309:SYS12297:IFPEEK(512)=1THENE=1:GOTO580
510 OPEN1,U,15,"i"+STR$(D):OPEN2,U,1,STR$(D)+":"+F$+","+MID$("psp",I,1)+",w":INPUT#1,X:IFX>20THENE=1:GOTO580:ELSESLEEP1:SYS59074
520 SYS12309:SYS12291:SLEEP1:POKE2585,PEEK(2584):GOTO580
550 E=0:GOSUB590:OPEN1,U,15:OPEN2,U,VAL(MID$("020",I,1)),STR$(D)+":"+F$+","+MID$("psp",I,1)+",r"
560 INPUT#1,X:IFX>20THENE=1:GOTO580:ELSESYS12309:POKE14363,I:SYS12300:IFPEEK(512)=1THENE=1:GOTO580
570 SYS12309:POKE14360,255:SYS12294
580 CLOSE1:CLOSE2:PRINT:SLEEP5:IFPEEK(215)THENFAST:RETURN:ELSERETURN
590 IFBA<2399THENSLOW:RETURN:ELSERETURN
599 REM--------------------- LOGOFF ------------------------------------
600 ONO(14)GOSUB8245:FORI=1TO4:CLOSEI:NEXT:IFPEEK(2824)=0THENO$=T$(9):GOSUB4
605 O$="{ct k}Planet Ink/Elite Software{ct k}2968 W. Ina Rd. Suite 140{ct k}Tucson, Az 85741{ct k}":GOSUB4
610 IFID<1THEN660:ELSEO(4)=O(4)+1:O(0)=O(0)+1:GOSUB400:U(3)=BA:U$(6)=O$(2):OPEN1,D(1,0),15:OPEN2,D(1,0),2,STR$(D(1,1))+":sys.userlog,s,a":M6$=U$(1):M7$=O$(3)
620 INPUT#1,I:IFI<20THEN630:ELSECLOSE2:OPEN2,D(1,0),2,STR$(D(1,1))+":sys.userlog,s,w"
630 SYS8216:O$="{cyan}":A$="":IFU$(5)="A"THENO$="{white}"
640 PRINT#2,USINGO$+"{reverse on}########{reverse off}  {reverse on}#######{reverse off}-{reverse on}#######{reverse off} {reverse on}####{reverse off} {reverse on}###{reverse off} {reverse on}################{reverse off} {reverse on}################{reverse off}";O$(3),O$(1),I$,BA,ID,U$(1),U$(3)
650 FORI=1TOP0:PRINT#2,P0$(I):P0$(I)="":NEXTI:CLOSE2:CLOSE1:A$="":OPEN2,D(4,0),2,STR$(D(4,1))+":sys.userfiles":RECORD#2,ID:A$="":FORI=1TO9:A$=A$+U$(I)+R$:NEXT
655 FORI=1TO3:A$=A$+STR$(U(I))+R$:NEXT:PRINT#2,A$;:A$="":RECORD#2,M(14)+1:FORI=2TO6:A$=A$+STR$(O(I))+R$:NEXTI:PRINT#2,A$:CLOSE2:P0=1:IFO(15)=0THENGOSUB13
660 U=D(0,0):D=D(0,1):DCLEARU(U),D(D):POKE2896,0:GOSUB300:I$="prg.logon":GOTO23
748 REM----------------------- LOCAL SYSOP COMMANDS ------------------------
749 GOSUB5:IFINSTR(I$,"e")THENX=-1:RETURN:ELSEX=VAL(I$):RETURN
751 DO:GETA$:LOOPUNTILA$="":PRINTR$"{yellow}{ct n}SYSCMD>{white}";:DO:I=PEEK(213):LOOPUNTILI<>88:POKE2823,1:DO:GETA$:LOOPUNTILA$=""
753 IFI=56THENPRINT"NEW ACCESS> ";:X=1:GOSUB5:X=ASC(I$):IFX>192ANDX<219THENU$(5)=I$:GOTO751:ELSEIFI=56THEN751
755 IFI=59THENPRINT"FLAG LETTER> ";:X=1:GOSUB5:X=ASC(I$):IFX>192ANDX<219THENX=X-192:MID$(U$(7),X,1)=RIGHT$(STR$(ABS(VAL(MID$(U$(7),X,1))-1)),1):GOTO751
760 IFI<>8THEN765:ELSEW1=ABS(W1-1):PRINT"LOCAL IS O";:IFW1THENPRINT"N.":GOTO799:ELSEPRINT"FF.":GOTO799
765 IFI=11THENPRINT"MINS REMAINING ({yellow}1-255{white})> ";:X=3:GOSUB749:IFX>0ANDX<256THENPOKE2825,X:POKE2824,1:GOTO751:ELSEIFI=11GOTO751
770 IFI=16THENPRINT"CALLS TODAY> ";:X=2:GOSUB749:IFX>=0ANDX<100THENU$(9)=STR$(X):GOTO751
775 IFI=19THENPRINT"SYSTEM CREDITS> ";:X=5:GOSUB749:IFX>=0THENU(2)=X:GOTO751
780 IFI=24THENPRINT"BLOCK CREDITS> ";:X=5:GOSUB749:IFX>=0THENU(1)=X:GOTO751
785 IFI<>55THEN799:ELSEPRINT"MENU>"R$"{down}1) access"R$"2) access flags"R$"3) local toggle"R$"4) timeleft"R$"5){sh space}calls"
790 PRINT"6) system credits"R$"7) block credits":GOTO751
799 POKE2896,0:GOSUB100:POKE2823,W1:PRINT"{white}done!"R$:RETURN
899 REM ------------------- MAIN MENU ROUTINES -------------------------------
900 O$(13)="":CLOSE1:CLOSE2:OPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,r":INPUT#1,I:IFI>0THENCLOSE1:CLOSE2:O$=T$(10):GOSUB4:GOTO920
905 SYS8213:SYS8222:A=ASC(I$):SYS8222:O$(10)=I$:SYS8222:O$(8)=I$:SYS8222:O$(9)=I$:I=0:DO:I=I+1:FORX=0TO4:SYS8222:M$(I,X)=I$:NEXTX:LOOPUNTILPEEK(253)ORI=30
910 O$(16)=F$+","+STR$(U)+","+STR$(D):CLOSE1:CLOSE2:C$="":GOSUB400:FORX=1TOI:IFM$(X,0)="{ct k}"THENM$(X,0)=R$
912 C$=C$+M$(X,0):NEXTX:O$(7)=C$:IFA=48THEN920:ELSEIFA>49THENO$(13)=CHR$(A):A=0
915 IFA>0THENGOSUB25:IFPEEK(2859)>0THENA$=CHR$(PEEK(2859)):I=INSTR(O$(7),A$):GOTO930
920 IFO$(13)<>""THENA$=O$(13):O$(13)="":I=INSTR(O$(7),A$):IFI>0THEN930
925 E=0:O$=O$(8+PEEK(2829))+"{ct c}":GOSUB4:C$=O$(7):GOSUB28:O$=M$(I,2):GOSUB4
930 IFMID$(M$(I,1),ASC(U$(5))-192,1)="0"THENO$="{ct k}":GOSUB4:GOTO920:ELSEM1$=M$(I,3):M2$=M$(I,4):O$=O$(10)+":"+A$:GOSUB7
940 IFM1$=""THENO$(13)="":GOTO920:ELSEI=INSTR(M1$,"{f4}"):IFI<1THENW1=VAL(M1$):M1$="":GOTO950
945 W1=VAL(LEFT$(M1$,I-1)):M1$=RIGHT$(M1$,LEN(M1$)-I)
950 IFW1>100THEN960:ELSEONW1GOTO8040,8050,600,8100,8260,8270,8280,8290,8410,8440,8450,8460,8480,8500,8510,8530,8550,8590,8610,8660
955 ON(W1-20)GOTO8690,8700,8730,8740,8750,8760,8770,8790,8810,8820,8860,8890,8900,8910,8920,8930,8990
960 U=D(0,0):D=D(0,1):IFW1>100ANDW1<201THENI$="prg.msg bases":GOTO23
965 IFW1>200ANDW1<301THENI$="prg.u/d bases":GOTO23
970 IFW1>300ANDW1<401THENI$="prg.voting booth":GOTO23
975 IFW1>400ANDW1<501THENI$="prg.sysop":GOTO23:ELSE940
990 IFM2$=""THENM3$="*":RETURN:ELSEI=INSTR(M2$,"{f4}"):IFI<1THENM3$=M2$:M2$="":RETURN
995 M3$=LEFT$(M2$,I-1):M2$=RIGHT$(M2$,LEN(M2$)-I):IFM3$=""THENM3$="*":RETURN:ELSERETURN
999 REM  ------------------ MESSAGE MAKER ----------------------------
1000 O(8)=200:IFPEEK(2915)THENO(8)=100
1010 CLOSE1:CLOSE2:CLOSE3:O$="{ct n}":POKE2850,1:O(13)=0:IFO(7)=1THENO$=CHR$(142)
1020 POKE2925,0:GOSUB9:IFO(9)=0THENFORI=0TO201:TT$(I)="":NEXTI:L=0
1030 A=FRE(1):O$="{ct k}{black}{clear}message maker!{ct k}{black}type /h for help{ct k}{black}"+MID$(STR$(O(8)),2)+" lines max. go:{white}":GOSUB4:E=0:O(9)=0:POKE2917,0:POKE2850,1:O(11)=0:W3=0
1040 IFPEEK(2825)<13THENO$="{black}["+MID$(STR$(PEEK(2825)),2)+" minutes left]{white}":GOSUB4
1100 O(13)=0:X=L:ONPEEK(2917)GOSUB1130:GOSUB6
1110 TT$(L)=I$:W1$="":W2$=LEFT$(TT$(L),2):IFINSTR(W2$,"/")=1ORINSTR(W2$,".")=1THENW1$=RIGHT$(W2$,1):IFASC(W1$)>64ANDASC(W1$)<91THENW1$=CHR$(ASC(W1$)+128)
1120 ONINSTR("HSEDLNVIAGWTCR/.",W1$)+1GOTO1150,1160,1200,1300,1350,1400,1420,1430,1450,1460,1480,1490,1510,1530,1700,1600,1600:GOTO1150
1130 O$=RIGHT$("  "+STR$(X),3)+">{ct c}":GOSUB4:RETURN
1150 IFL>=O(8)ORFRE(1)<500THENO$="{ct k}buffer full. type /h for help":GOSUB4:GOTO1100:ELSEL=L+1:GOTO1100
1160 OPEN2,D(1,0),2,STR$(D(1,1))+":sys.msg help,s,r":SYS8210:CLOSE2:GOTO1100
1170 IFO(13)THEN1650
1175 E=0:W1=0:X=-1:Y=-1:W1$=TT$(L):W2$=RIGHT$(W1$,LEN(W1$)-2):W2=INSTR(W2$,","):IFINSTR(W2$,"e")OR(LEN(W2$)>4ANDW2=0)THENE=1:O$="bad format":GOSUB4:GOTO1190
1176 IFW2=0THENW2=LEN(W2$)
1180 IFW2$=""THENW1=1:X=0:ELSEX=VAL(LEFT$(W2$,W2)):IFW2<>LEN(W2$)THENY=VAL(RIGHT$(W2$,LEN(W2$)-W2)):IF Y<X OR Y>L-1 THENY=X
1185 IFX>L-1ORX<0THENE=1:IFW3=0THENO$="bad line number":GOSUB4
1190 W3=0:RETURN
1200 O$="save (y/n)?{ct c}":GOSUB4:C$="YN":POKE2831,1:GOSUB28:O$=CHR$(ASC(A$)AND127):GOSUB4:IFI=2THEN1100:ELSEFORX=0TOL:I=I+LEN(TT$(X))+2:NEXTX:I=INT(I/256)
1210 O$="saving...{ct c}":GOSUB4:OPEN2,U,0,"$"+STR$(D)+":zu%=99+":SYS8225:CLOSE2:IFPEEK(2854)+256*PEEK(2855)<ITHENO$=" no disk space!":GOSUB4:O(11)=1:GOTO1470
1220 OPEN1,U,15:IFO(12)=1THENPRINT#1,"s"+STR$(D)+":"+F$:GOTO1260:ELSEIFO(12)<>2THEN1260
1230 OPEN2,U,2,STR$(D)+":"+F$+",s,r":INPUT#1,I:IFI>19THEN1299:ELSEPRINT#1,"s"+STR$(D)+":sys.work":OPEN3,U,3,STR$(D)+":sys.work,s,w":INPUT#1,I:IFI>19THEN1299
1240 IFO(10)=0THEN1272:ELSEFORX=0TOO(10)-1:IFX<>0THENPRINT#3,CHR$(1)
1250 GOSUB31:NEXTX:PRINT#3,CHR$(1):GOTO1272
1260 OPEN3,U,3,STR$(D)+":"+F$+",s,w":INPUT#1,I:IFI>0THENCLOSE3:OPEN3,U,3,STR$(D)+":"+F$+",s,a":PRINT#3,CHR$(1)
1272 IFO$(5)<>""THENPRINT#3,O$(5):PRINT#3,"{white}{f4}"
1274 TT$(L)="":IFL=0THENTT$(L)=" ":ELSEIFO(7)=1THENPRINT#3,CHR$(142)
1280 FORI=0TOL-1:PRINT#3,TT$(I):NEXTI:IFO(12)<>2THENCLOSE3:CLOSE1:O$="{left*6}ed!   ":GOSUB4:GOTO1470
1285 SYS8213:IFPEEK(253)=0THENDO:PRINT#3,CHR$(1):GOSUB31:LOOPUNTILPEEK(253)
1290 CLOSE3:CLOSE2:INPUT#1,I:IFI<20THENPRINT#1,"s"+STR$(D)+":"+F$:PRINT#1,"r"+STR$(D)+":"+F$+"=sys.work":O$="{left*6}ed!   ":GOSUB4:GOTO1470:ELSE1299
1299 CLOSE3:CLOSE2:PRINT#1,"s"+STR$(D)+":sys.work":PRINT#1,"v"+STR$(D):O(11)=1:O$=" disk error, please wait!":GOTO1470
1300 GOSUB1170:IFE=1ORW1=1THEN1100:ELSEO$="{white}>"+TT$(X)+"{ct k}search for: {ct c}":GOSUB4:GOSUB6:W1$=TT$(X):W1=LEN(W1$):W2=LEN(I$):IFW1=0THEN1100
1310 IFINSTR(W1$,I$)=0THENO$="not found!":GOSUB4:GOTO1100:ELSEY=INSTR(W1$,I$):W2=W1-Y-W2+1:W2$=LEFT$(W1$,Y-1):W3$=RIGHT$(W1$,W2)
1320 O$="change to : {ct c}":GOSUB4:GOSUB6:TT$(X)=W2$+I$+W3$:IFTT$(X)=""THENTT$(X)="":ELSEIFLEN(TT$(X))>120THENO$="illegal length":GOSUB4:TT$(X)=W1$
1330 GOTO1100
1350 W3=1:GOSUB1170:IFW1=1ORE=1ANDX>=0THEN1100:ELSEIFE=1ANDX<0THENX=L+X:Y=L-1:E=0:IFX<0THENE=1
1360 IFE=1THEN1100:ELSEIFY=-1ANDINSTR(W2$,",")THENY=L-1:ELSEIFY=-1THENY=X
1370 I$="":IFX<>YTHENI$="s"
1380 W1=Y-X+1:FORI=XTOL-W1+1:TT$(I)=TT$(I+W1):NEXTI:L=L-W1:IFL<0THENL=0
1390 I=FRE(0):O$=STR$(W1)+" line"+I$+" deleted":GOSUB4:GOTO1100
1400 GOSUB1170:O$="{white}":GOSUB9:IFE=1THEN1100:ELSEIFW1=1ORY<0THENY=L-1
1410 DO:I=X:GOSUB1130:O$=TT$(X)+CHR$(0):SYS8204:GOSUB8:X=X+1:LOOPUNTILX>YORA$=" ":GOTO1100
1420 O$="clear (y/n)?{ct c}":GOSUB4:C$="YN":POKE2831,1:GOSUB28:O$=CHR$(ASC(A$)AND127):GOSUB4:ONIGOTO1010,1100
1430 GOSUB1170:O$="{white}":GOSUB9:IFE=1THEN1100:ELSEIFW1=1ORY<0THENY=L-1
1440 DO:O$=TT$(X):GOSUB4:GOSUB8:X=X+1:LOOPUNTILX>YORA$=" ":GOTO1100
1450 GOSUB1170:IFE=1ORL>O(8)-1ORW1=1THEN1100:ELSEFORI=LTOX+1STEP-1:TT$(I)=TT$(I-1):NEXTI:TT$(X)="":L=L+1:I=FRE(1):GOTO1100
1460 O$="abort (y/n)?{ct c}":GOSUB4:C$="YN":POKE2831,1:GOSUB28:O$=CHR$(ASC(A$)AND127):GOSUB4:IFI=2THEN1100:ELSEO(11)=1
1470 O$(5)="":O(7)=0:O(9)=0:O(10)=0:O(12)=0:E=0:POKE2850,0:POKE2917,0:FORI=0TOO(8):TT$(I)="":NEXTI:O(13)=0:CLOSE2:CLOSE3:CLOSE1:O$="{ct n}":POKE2850,1:GOSUB4
1475 POKE2925,M(3):RETURN
1480 IFPEEK(2829)=0THEN1100:ELSEIFO(7)=1THENO(7)=0:O$="{ct n}gfx off":GOSUB4:GOTO1100:ELSEO(7)=1:O$=CHR$(142)+"gfx on":GOSUB4:GOTO1100
1490 IFPEEK(2850)THENPOKE2850,0:O$="word wrap off":GOSUB4:GOTO1100:ELSEPOKE2850,1:O$="word wrap on":GOSUB4:GOTO1100
1510 IFPEEK(2917)=1THENPOKE2917,0:O$="line numbers off":GOSUB4:GOTO1100:ELSEPOKE2917,1:O$="line numbers on":GOSUB4:GOTO1100
1530 GOSUB1170:IFE=1THEN1100:ELSEIFW1=1THENY=L-1:ELSEIFY<0THENY=X
1535 W4=(PEEK(2915)*40)+39:IFPEEK(2917)THENW4=W4-1
1540 FORW1=XTOY:IFTT$(W1)=""ORTT$(W1)=""THEN1560:ELSEW2=0:I$=TT$(W1):FORI=1TOLEN(I$):IFMID$(I$,I,1)<>" "ANDW2=0THENW2=I
1550 NEXTI:I$=RIGHT$(I$,LEN(I$)-(W2-1)):I=LEN(I$):FORW3=0TO((((W4+1)/2)-(I/2))-1):I$=" "+I$:NEXTW3:TT$(W1)=I$
1560 NEXTW1:I=FRE(1):GOTO1100
1595 IFI=0THENO$=O$+" off":RETURN:ELSEO$=O$+" on":RETURN
1600 O$="{ct k}{black}at line:"+STR$(L)+" (l/n:":I=PEEK(2917):GOSUB1595:O$=O$+"){f7}(w/w:":I=PEEK(2850):GOSUB1595:O$=O$+") (gfx:":I=O(7):GOSUB1595:O$=O$+"){ct k}":GOSUB4
1610 O$="{black}h)elp file  s)ave text  e)dit line  {f7}d)elete     l)ist text  n)ew  (clear)":GOSUB4
1620 O$="v)iew text  i)nsert     a)bort      {f7}g)fx toggle w)w toggle  t)oggle line #s":GOSUB4:O$="c)enter     r)eplace line":GOSUB4
1630 O$="{ct k}{black}command (return = exit): {ct c}":GOSUB4:A$="":DO:GOSUB8:A$=CHR$(ASC(A$)OR128):LOOPUNTILINSTR("HSEDLNVIAGWTCR"+CHR$(141),A$)>0
1640 O$=CHR$(ASC(A$)AND127):GOSUB4:O(13)=1:ONINSTR("HSEDLNVIAGWTCR"+CHR$(141),A$)GOTO1160,1200,1300,1350,1400,1420,1430,1450,1460,1480,1490,1510,1530,1700,1100
1650 E=0:X=-1:Y=-1:W1=0:IFINSTR("EIR",A$)THEN1680:ELSEO$="from line: {ct c}":GOSUB4:GOSUB10:IFI<0ORI>L-1THENE=1:RETURN:ELSEIFI$=""THENW1=1:I=0:X=0:GOTO1670
1660 X=I:O$="  to line: {ct c}":GOSUB4:GOSUB10:IFI$<>""ANDI>0THENIFI<X ORI>L-1THENI=X
1670 Y=I:RETURN
1680 O$="which line: {ct c}":GOSUB4:GOSUB10:IFI<0ORI>L-1THENE=1:RETURN:ELSEIFI$=""THENW1=1:I=0:X=0:GOTO1670:ELSEX=I:RETURN
1700 GOSUB1170:IFE=1ORW1=1THEN1100:ELSEONPEEK(2917)GOSUB1130:O$="rewrite line"+STR$(X):GOSUB4:ONPEEK(2917)GOSUB1130:O$=TT$(X):GOSUB4:GOSUB6
1710 IFI$=""THENO$="(no change)":GOSUB4:GOTO1100:ELSETT$(X)=I$:GOTO1100
1999 REM ------------------------ WELCOME THE LOSER --------------------------
2000 SYS8216:O$(1)=I$:POKE2915,VAL(MID$(U$(8),2,1)):IFLEFT$(O$(2),6)<>LEFT$(U$(6),6)THENU$(9)=" 1":GOSUB400
2020 GOSUB100:O$="Sub:":GOSUB250:POKE2825,CD(ASC(U$(5))-192,1):POKE2824,1:IFVAL(U$(9))>CD(ASC(U$(5))-192,0)THENO$=T$(11):GOSUB4:GOTO600
2040 F$="sys.welcome":U=D(1,0):D=D(1,1):GOSUB40
2060 X=0:OPEN1,D(1,0),15:OPEN2,D(1,0),2,STR$(D(1,1))+":sys.news,s,r":INPUT#1,I:IFI>20THENCLOSE1:CLOSE2:O$="{black}No news.":GOSUB4:GOTO2160
2080 SYS8222:IFU$(6)>I$THEN2120:ELSEA$=I$:SYS8222:SYS8222:IFMID$(I$,ASC(U$(5))-192,1)="0"THEN2120
2100 SYS8210:X=X+1:O$="RETURN>{ct c}":GOSUB4:C$=R$:GOSUB28:O$=R$:GOSUB4:IFPEEK(255)THEN2080:ELSE2140
2120 SYS8213:IFPEEK(253)THEN2140:ELSE2080
2140 CLOSE1:CLOSE2:IFX=0THENO$="{ct k}{black}No new news.":GOSUB4
2160 IFBA>=U(3)ORU(3)=0ORU(3)>1199ORBA>1199THEN2200:ELSEO$="{ct k}{black}Change back to"+STR$(U(3))+" baud (y/n)? {ct c}":GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN2200
2180 O$="{ct k}{black}Set your term for"+STR$(U(3))+" baud,{f7}and press return.":GOSUB4:I=U(3):GOSUB450:C$=R$:GOSUB28:GOSUB100
2200 OPEN1,D(5,0),15:OPEN2,D(5,0),2,STR$(D(5,1))+":mail."+STR$(ID)+",s,r":INPUT#1,I:IFI>20THENCLOSE2:CLOSE1:O$=T$(12):GOSUB4:GOTO2260
2220 CLOSE1:CLOSE2:O$=T$(13):GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN2260
2240 U1=D(5,0):D1=D(5,1):F1$="mail."+STR$(ID):OPEN1,U1,15,"s"+STR$(D1)+":mail.work":CLOSE1:GOSUB8130
2260 F$="menu.main":U=D(1,0):D=D(1,1):GOTO900
7999 REM ------------------- MAIN OPCODE ROUTINES ------------------------
8000 GOSUB990:IFM3$<>"*"THEN8005:ELSEO$=T$(2):GOSUB4:POKE2895,1:POKE2847,16:GOSUB6:F$=I$:IFF$=""THENE=1:RETURN:ELSERETURN
8005 F$=M3$:IFRIGHT$(F$,2)="/a"ANDPEEK(2829)THENF$=LEFT$(F$,LEN(F$)-2):RETURN:ELSERETURN
8010 GOSUB990:IFM3$<>"*"THENU=VAL(M3$):GOSUB990:D=VAL(M3$):RETURN:ELSEO$=T$(3):GOSUB4:POKE2895,1:POKE2847,2:GOSUB10:U=I:IFI<8ORI>30THENU=8
8020 GOSUB990:O$=T$(4):GOSUB4:POKE2895,1:POKE2847,2:GOSUB10:D=I:IFD<0ORD>9THEND=0:RETURN:ELSERETURN
8030 GOSUB8000:IFETHENGOSUB990:GOSUB990:RETURN:ELSEGOSUB8010:RETURN
8040 GOSUB8000:ONEGOTO920:GOSUB990:O$(15)=O$(16):IFM3$="*"THENU=D(1,0):D=D(1,1):GOTO900:ELSEU=VAL(M3$):GOSUB990:D=VAL(M3$):GOTO900
8050 GOSUB8030:ONEGOTO920:OPEN2,U,2,STR$(D)+":"+F$+",p,r":GET#2,A$:IFSTTHENCLOSE2:O$=T$(5):GOSUB4:GOTO920:ELSECLOSE2:I$=F$:GOTO23
8060 O$=T$(6):GOSUB4:POKE2831,1:POKE2847,16:POKE2895,1:GOSUB10:W1$=I$:IFI$=""ORI>M(14)ORI=IDTHENA=0:RETURN:ELSEIFI>0THENA=I:GOTO8080
8070 A=0:O$=T$(7):GOSUB4:DO:A=A+1:A$=UI$(A):LOOPUNTILA$=W1$ORA>M(14)ORA$="":IFA$<>W1$ORA$=U$(1)ORA>M(14)THENO$=T$(8):GOSUB4:A=0:RETURN
8080 IFUI$(A)="^"ORUI$(A)=""ORA>M(14)THENA=0:RETURN
8090 IFI$=UI$(A)THENRETURN
8095 O$="{ct k}{black}Is "+UI$(A)+" correct (y/n)? {ct c}":GOSUB4:C$="NY":GOSUB28:O$=A$:GOSUB4:IFI=1THENA=0:RETURN:ELSERETURN
8100 GOSUB990:IFM3$<>"*"THENA=VAL(M3$):GOSUB8080:ELSEGOSUB8060:I=FRE(0)
8110 GOSUB8120:GOTO940
8120 IFA=0ORA=IDTHENRETURN:ELSECLOSE1:U=D(5,0):D=D(5,1):F$="mail."+STR$(A):GOSUB400:O$(5)=U$(1)+R$+STR$(ID)+R$+O$(3):GOSUB1000:RETURN
8130 O$=T$(7):GOSUB4:W5=0:O(14)=1:W7=O(2)
8140 CLOSE1:CLOSE2:OPEN1,U1,15:OPEN2,U1,2,STR$(D1)+":"+F1$+",s,r":INPUT#1,I:IFI>20THENO$=T$(16):GOSUB4:CLOSE2:W7=0:CLOSE1:O(14)=0:RETURN
8150 IFW5=0THEN8160:ELSEFORI=1TOW5:SYS8213:NEXTI:IFPEEK(253)THEN8240
8160 SYS8222:W5$=I$:SYS8222:W6=VAL(I$):SYS8222:C$=""
8170 O$="{clear}{ct k}{yellow}Sent by: {white}"+W5$+"{yellow} (ID#{white}"+STR$(W6)+"{yellow}){ct k}     At: {white}"+I$:GOSUB4:SYS8210
8180 CLOSE2:O(14)=1:O$="{ct n}{ct k}{black}R)eply, H)old, A)gain,{f7}Q)uit, or N)ext: {ct c}":GOSUB4:C$="RHANQ":GOSUB28:O$=A$:GOSUB4:IFI=1THENA=W6:GOSUB8120:GOTO8140
8190 IFI=3THEN8140:ELSEIFI=5THENPRINT#1,"s"+STR$(D1)+":mail.work":INPUT#1,I:W7=O(2):GOTO8250
8200 IFI=4THENO$=T$(14):GOSUB4:W5=W5+1:W7=W7-1:GOTO8140:ELSEO$="Saving...{ct c}":GOSUB4:OPEN3,U1,3,STR$(D1)+":mail.work,s,a":PRINT#3,CHR$(1)
8210 INPUT#1,I:IFI>20THENCLOSE3:OPEN3,U1,3,STR$(D1)+":mail.work,s,w"
8220 OPEN2,U1,2,STR$(D1)+":"+F1$+",s,r":IFW5=0THEN8230:ELSEFORI=1TOW5:SYS8213:NEXTI:IFPEEK(253)THEN8240
8230 GOSUB31:CLOSE2:CLOSE3:O$="{left*6}ed!    {ct k*2}"+T$(14):GOSUB4:W5=W5+1:GOTO8140
8240 CLOSE3:CLOSE2:O$=T$(15):GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THENI=5:GOTO8190
8245 CLOSE1:OPEN1,U1,15,"s"+STR$(D1)+":"+F1$:PRINT#1,"r"+STR$(D1)+":"+F1$+"=mail.work":INPUT#1,I
8250 CLOSE1:CLOSE3:CLOSE2:O(14)=0:RETURN
8260 U1=D(5,0):D1=D(5,1):F1$="mail."+STR$(ID):OPEN1,U1,15,"s"+STR$(D1)+":mail.work":CLOSE1:GOSUB8130:GOTO940
8270 U=D(6,0):D=D(6,1):F$="sys.feedback":GOSUB400:O$(5)=U$(1)+R$+STR$(ID)+R$+O$(3):GOSUB1000:IFO(11)=0THENO(2)=O(2)+1:GOTO940:ELSE920
8280 F1$="sys.feedback":U1=D(6,0):D1=D(6,1):OPEN1,U1,15,"s"+STR$(D1)+":mail.work":CLOSE1:GOSUB8130:O(2)=W7:IFO(2)<0THENO(2)=0:GOTO940:ELSE940
8290 GOSUB8030:IFETHENGOSUB990:GOTO920:ELSEE=0:GOSUB990
8300 IFM3$="-"THENO(12)=1:ELSEIFM3$="*"THENGOSUB8400:GOTO8310:ELSEIFM3$="+"THENO(12)=0:GOTO8320:ELSEO(10)=VAL(M3$):O(12)=2
8310 GOSUB8330:IFO(11)=1THEN920
8320 GOSUB1000:GOTO940
8330 O(11)=0:O$(5)="":L=0:OPEN1,U,15:OPEN2,U,2,F$+",s,r":INPUT#1,I:IFI>20THENCLOSE1:CLOSE2:O$=T$(16):GOSUB4:O(12)=1:RETURN
8340 IFO(12)<>2THEN8350:ELSEIFO(10)=0THEN8350:ELSEFORX=0TOO(10)-1:SYS8213:NEXTX:IFPEEK(253)THENCLOSE2:CLOSE1:O$=T$(17):GOSUB4:O(11)=1:RETURN
8350 O$=T$(18):GOSUB4
8360 SYS8222:IFPEEK(253)THENCLOSE2:CLOSE1:A=FRE(1):TT$(L)=I$:L=L+1:O(9)=1:RETURN
8370 IFI$=CHR$(1)ANDO(12)=2THENCLOSE2:CLOSE1:A=FRE(1):O(9)=1:RETURN
8380 IFI$=CHR$(142)THENO(7)=1:ELSEIFI$="{white}{f4}"THENFORI=0TOL-1:O$(5)=O$(5)+TT$(I)+R$:NEXTI:O$(5)=LEFT$(O$(5),LEN(O$(5))-1):L=0:GOTO8360
8390 TT$(L)=I$:L=L+1:GOTO8360
8400 O$=T$(19):GOSUB4:GOSUB10:O(10)=I:O(12)=2:RETURN
8410 GOSUB8030:IFETHENGOSUB990:GOTO920:ELSEGOSUB990:I=0:IFM3$="*"THENGOSUB8400:O(10)=0:O(12)=0
8420 IFI=0THENGOSUB25:GOTO940:ELSEOPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,r":FORX=1TOI:SYS8213:NEXTX:SYS8210:INPUT#1,I:CLOSE1:CLOSE2:IFPEEK(252)=0THEN940
8430 GOSUB27:GOTO940
8440 GOSUB990:C$=M3$:GOSUB990:C$=C$+M3$:GOSUB28:O$=A$:GOSUB4:IFINSTR(M3$,A$)THEN940:ELSEGOTO920
8450 GOSUB990:POKE2831,1:POKE2844,1:GOSUB6:IFI$=M3$THEN940:ELSEO$=T$(20):GOSUB4:GOTO920
8460 POKE2829,ABS(PEEK(2829)-1):IFPEEK(2829)=1THENO$=T$(21):ELSEO$=T$(22)
8470 GOSUB4:GOSUB300:GOTO940
8480 POKE2845,ABS(PEEK(2845)-1):IFPEEK(2845)=1THENO$=T$(23):ELSEO$=T$(24)
8490 GOSUB4:GOTO940
8500 GOSUB990:IFPEEK(2829)=VAL(M3$)THEN940:ELSEGOTO920
8510 GOSUB990:Y=0:FORX=1TO26:IFMID$(U$(7),X,1)="1"ANDMID$(M3$,X,1)="1"THENY=1
8520 NEXTX:IFYTHEN940:ELSEGOTO920
8530 GOSUB990:Y=0:FORX=1TO26:IFMID$(M3$,X,1)="1"ANDMID$(U$(7),X,1)="0"THENY=1
8540 NEXTX:IFYTHEN920:ELSGOTO940
8550 GOSUB990:X=VAL(M3$):IFX=0THENPOKE2896,ABS(PEEK(2896)-1):ELSEPOKE2896,1
8560 GOSUB300:IFPEEK(2896)=1THENO$=T$(25):GOSUB4:POKE2831,1:POKE2847,PEEK(238)-15:POKE2895,1:GOSUB6:IFPEEK(2896)=2THEN940
8570 O$="Sub: "+I$:GOSUB250:IFPEEK(2896)THENO$=T$(26):GOSUB4:FORX=1TO9:O$="{black}.{ct g}{ct c}":GOSUB4:SLEEP1:NEXTX:O$="{black}.{ct g}":ELSEO$=T$(27)
8580 GOSUB4:GOTO940
8590 GOSUB990:Y=0:C$=M3$:IFC$="*"THENY=1:O$=T$(28):GOSUB4:GOSUB6:C$=I$:IFI$=""THENGOSUB990:GOSUB990:GOTO940
8600 GOSUB8010:CLOSE1:OPEN1,U,15:PRINT#1,C$:INPUT#1,A,I$,W1,W2:CLOSE1:IFYTHENO$="{ct k}{black}Status:"+STR$(A)+","+I$+","+STR$(W1)+","+STR$(W2):GOTO940:ELSE940
8610 GOSUB990:W1$=M3$:IFM3$="*"THENGOSUB8640:GOTO8620:ELSEIFM3$="#"THENW1$="*"
8620 GOSUB8010:GOSUB990:I=0:IFM3$="*"THENGOSUB8650:GOTO8630:ELSEIFM3$="#"THENW1$=CHR$(U1+192)+W1$:I=U1+192:U=D(8,0):D=D(8,1):ELSEI=VAL(M3$)
8630 OPEN2,U,0,"$"+STR$(D)+":"+W1$:POKE253,I:SYS8219:CLOSE2:GOTO940
8640 O$=T$(29):GOSUB4:POKE2847,16:POKE2895,1:GOSUB6:IFI$=""THENW1$="*":RETURN:ELSEW1$=I$:RETURN
8650 O$=T$(30):GOSUB4:POKE2847,1:POKE2895,1:GOSUB6:IFI$=""THENI=0:RETURN:ELSEI=ASC(I$):RETURN
8660 GOSUB8030:IFETHENGOSUB990:GOTO920:ELSEGOSUB990:I=0:IFM3$="*"THENGOSUB8400:O(10)=0:O(12)=0
8670 OPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,r":INPUT#1,X:IFX>0THENCLOSE1:CLOSE2:GOTO920:ELSEIFI>0THENFORX=1TOI:SYS8213:NEXTX
8680 SYS8222:IFPEEK(253)ORI$=CHR$(1)THENCLOSE2:CLOSE1:O$=I$:GOSUB4:I=FRE(1):GOTO940:ELSEO$=I$:GOSUB4:GOTO8680
8690 O(1)=1:I$="prg.new user":U=D(0,0):D=D(0,1):GOTO23
8700 O$=T$(31):GOSUB4:POKE2895,1:POKE2847,16:POKE2831,1:GOSUB10:IFI=0ANDI$<>""THEN8710:ELSEIFI=0ORI>M(14)THENI=1
8710 O$=R$:GOSUB9:X=I:DO:IFIORLEFT$(UI$(X),LEN(I$))=I$THENIFUI$(X)<>"^"ANDUI$(X)<>""THENO$="{black}"+MID$(STR$(X),2)+") "+UI$(X):GOSUB4
8720 X=X+1:GOSUB8:LOOPUNTILUI$(X)=""ORA$=" ":GOTO940
8730 O(15)=1:GOTO600
8740 GOSUB990:IFM3$="*"THENO$=T$(32):GOSUB4:X=38:GOSUB5:O$(12)=I$:GOTO940:ELSEO$(12)=M3$:GOTO940
8750 GOSUB990:O$=M3$+"{ct c}":GOSUB4:GOTO940
8760 GOSUB990:A$=M3$:I=INSTR(O$(7),A$):IFITHEN930:ELSEGOTO940
8770 O$="{ct k}{black}Current baud is"+STR$(BA)+",{f7}enter new baud rate: {ct c}":GOSUB4:GOSUB10:IFI<100ORI>1199ORBA>1199THENO$="{ct k}Out of range.":GOSUB4:GOTO920
8780 O$="{ct k}{black}Set your term for"+STR$(I)+" baud,{f7}and press return.":GOSUB4:GOSUB450:C$=R$:GOSUB28:O$=R$:GOSUB4:GOSUB100:GOTO940
8790 GOSUB990:W1$=M3$:IFM3$="*"THENGOSUB8640
8800 GOSUB8010:OPEN2,U,0,"$"+STR$(D)+":"+W1$:SYS8228:CLOSE2:O$="{ct k}{black}"+MID$(STR$(PEEK(2858)),2)+" files counted.":GOSUB4:GOTO940
8810 GOSUB8030:C$=O$(7):A=1:GOTO915
8820 GOSUB990:IFM3$="*"THEN920
8830 IFM3$=""THENC$=O$(7):GOSUB28:GOTO930:ELSEI=INSTR(M3$,";"):IFI<1THENX=VAL(M3$):M3$="":GOTO8850
8840 X=VAL(LEFT$(M3$,I-1)):M3$=RIGHT$(M3$,LEN(M3$)-I):IFM3$=""THEN920
8850 IFX=IDTHEN940:ELSE8830
8860 GOSUB990:IFM3$<>"*"THENW1=ASC(M3$)-192:GOTO8870:ELSEO$=T$(33):GOSUB4:X=1:GOSUB5:IFASC(I$)>192THENW1=ASC(I$)-192:GOTO8870:ELSE920
8870 GOSUB990:IFM3$<>"*"THENA=VAL(M3$):GOTO8880:ELSEO$="{ct k}{black}C)lear or S)et: {ct c}":GOSUB4:C$="CS"+R$:GOSUB28:O$=A$:GOSUB4:IFI=3THEN920:ELSEA=I-1
8880 MID$(U$(7),W1,1)=RIGHT$(STR$(A),1):GOTO940
8890 GOSUB990:O$(13)=M3$:GOTO940
8900 GOSUB990:U(2)=U(2)+VAL(M3$):GOTO940
8910 GOSUB990:O(VAL(M3$))=0:GOTO940
8920 GOSUB990:IFU(2)>=VAL(M3$)THEN940:ELSEO$=T$(34):GOSUB4:GOTO920
8930 GOSUB8030:POKE2930,0:POKE2931,0
8940 SYS8234:IFPEEK(2931)THENPOKE2931,0:GOTO920
8950 POKE2932,U:POKE2933,D:POKE2934,LEN(F$):FORI=1TOLEN(F$):POKE2934+I,ASC(MID$(F$,I,1)):NEXTI:O$(11)="":POKE2930,1
8960 O$="{reverse on}{light gray}"+LEFT$(F$+"            ",12):X=PEEK(238)-12:Y=24:GOSUB30:F$=""
8970 SYS8240:FORI=1TOPEEK(2934):F$=F$+CHR$(PEEK(2934+I)):NEXTI
8980 SYS15622:RUN(F$),U(PEEK(2932)),D(PEEK(2933))
8990 IFO$(15)=""THEN920:ELSEF$=O$(15):I=INSTR(F$,","):U=VAL(MID$(F$,I+1)):D=VAL(MID$(MID$(F$,I+1),INSTR(MID$(F$,I+1),",")+1)):F$=LEFT$(F$,I-1):GOTO900
