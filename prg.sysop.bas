!--------------------------------------------------
!- Monday, May 15, 2017 12:17:32 AM
!- Import of : 
!- c:\src\zelch128\prg.sysop.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
30000 SYS15625:REM ================= SYSOP MODULE V1.0 ============= 11/03/90 =
30010 ON(W1-400)GOTO30400,30400,32700,34700,36600,40800,41400:GOTO940
30100 GOSUB4:IFW1=401THENO$=R$:GOSUB9:RETURN
30200 IFTT$(Y)<>""THENC$=CHR$(20)+R$:GOSUB28:IFI=2THENO$=A$:GOSUB9:RETURN:ELSEIFTT$(Y)<>""THENFORX=1TOMX(Y):O$=CHR$(20):GOSUB9:NEXTX
30300 X=A:GOSUB5:IFI$=""THENRETURN:ELSETT$(Y)=I$:MX(Y)=LEN(I$):RETURN
30400 GOSUB8060:IFA=0THEN940
30500 W2=A:U=D(4,0):D=D(4,1):CLOSE2:OPEN2,U,2,CHR$(D)+":sys.userfiles":RECORD#2,A:FORX=1TO8:SYS8222:TT$(X)=I$:MX(X)=LEN(I$):NEXTX
30600 FORX=9TO12:SYS8222:A=VAL(I$):TT$(X)=MID$(STR$(A),2):MX(X)=LEN(TT$(X)):NEXTX:CLOSE2
30700 Y=1:A=16:O$="{ct k}{black}Handle      : {white}"+TT$(1)+"{ct c}":GOSUB30100
30800 Y=3:A=16:O$="{black}Real name   : {white}"+TT$(3)+"{ct c}":GOSUB30100
30900 Y=2:A=8:O$="{black}Password    : {white}"+TT$(2)+"{ct c}":GOSUB30100
31000 Y=4:A=14:O$="{black}Phone number: {white}"+TT$(4)+"{ct c}":GOSUB30100
31100 Y=5:A=1:O$="{black}Access level: {white}"+TT$(5)+"{ct c}":GOSUB30100
31200 Y=6:A=10:O$="{black}Last call   : {white}YYMMDDHHMM{ct k}            : "+TT$(6)+"{ct c}":GOSUB30100
31300 Y=9:A=2:O$="{black}Calls today : {white}"+TT$(9)+"{ct c}":GOSUB30100
31400 Y=10:A=6:O$="{black}Blocks      : {white}"+TT$(10)+"{ct c}":GOSUB30100
31500 Y=11:A=4:O$="{black}Credits     : {white}"+TT$(11)+"{ct c}":GOSUB30100
31600 Y=12:A=4:O$="{black}Default baud: {white}"+TT$(12)+"{ct c}":GOSUB30100
31700 Y=8:A=3:O$="{black}Info flags  : {white}E8D{ct k}            : "+TT$(8)+"{ct c}":GOSUB30100
31800 O$="{black}Flags: {white}ABCDEFGHIJKLMNOPQRSTUVWXYZ{ct k}     : {ct c}":GOSUB4:IFW1=401THENO$=TT$(7):GOSUB4:GOTO32300
31900 O$=TT$(7)+"{ct c}":GOSUB4:C$=CHR$(20)+R$:GOSUB28:IFI=2THENO$=R$:GOSUB9:GOTO32300
32000 FORX=1TO26:O$=CHR$(20):GOSUB9:NEXTX:O$="Toggle which: {ct c}":GOSUB4:Y=14:X=1:DO:POKE2831,1:GOSUB8:LOOPUNTILA$<>"":IFA$=R$THEN32200
32100 X=ASC(A$)-192:IFX>0ANDX<27THENY=15:O$=A$:GOSUB9:MID$(TT$(7),X,1)=MID$(STR$(ABS(VAL(MID$(TT$(7),X,1))-1)),2)
32200 FORX=1TOY:O$=CHR$(20):GOSUB9:NEXTX:GOTO31900
32300 IFW1<>402THEN940:ELSEO$="{ct k}{black}A)bort, S)ave, D)elete, E)dit : {ct c}":GOSUB4:C$="ASED":GOSUB28:O$=A$+R$:GOSUB4:ONIGOTO940,32400,30700,32350
32350 O$="{black}Are you sure (y/n)? {ct c}":GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN32300:ELSEOPEN1,D(5,0),15,"s"+STR$(D(5,1))+":mail."+STR$(W2):CLOSE1:I=4
32400 IFI=4THENO$="{ct k}Deleting...{ct c}":GOSUB4:TT$(1)="^":ELSEO$="Saving...{ct c}":GOSUB4
32500 OPEN2,U,2,CHR$(D)+":sys.userfiles":RECORD#2,W2:A$="":FORX=1TO12:A$=A$+TT$(X)+CHR$(13):NEXTX:UI$(W2)=TT$(1)
32600 PRINT#2,LEFT$(A$,LEN(A$)-1):CLOSE2:FORI=1TO6:O$=CHR$(20):GOSUB9:NEXTI:O$="ed!":GOSUB4:GOTO940
32700 GOSUB990:IFM3$="#"THEN32900:ELSEM1=ASC(M3$)-192:IFM1>0ANDM1<21THEN32900
32800 O$=T$(57):GOSUB4:X=1:GOSUB5:I=ASC(I$)-192:IFI<1ORI>20THEN920:ELSEM1=I
32900 U=D(1,0):D=D(1,1):OPEN2,U,2,CHR$(D)+":sys.msg bases":RECORD#2,M1:FORX=1TO5:SYS8222:TT$(X)=MID$(STR$(VAL(I$)),2):MX(X)=LEN(TT$(X)):NEXTX
33000 FORX=6TO8:SYS8222:TT$(X)=I$:MX(X)=LEN(TT$(X)):NEXTX:CLOSE2
33100 Y=6:A=20:O$="{ct k}{black}Subboard Name: {white}"+TT$(6)+"{ct c}":GOSUB30100
33200 Y=1:A=2:O$="{black}Device number: {white}"+TT$(1)+"{ct c}":GOSUB30100
33300 Y=2:A=1:O$="{black}LU/Drive     : {white}"+TT$(2)+"{ct c}":GOSUB30100
33400 Y=3:A=2:O$="{black}Max Msgs (50): {white}"+TT$(3)+"{ct c}":GOSUB30100:IFVAL(TT$(3))<1ORVAL(TT$(3))>50THEN33400
33500 Y=4:A=2:O$="{black}Max Responses: {white}"+TT$(4)+"{ct c}":GOSUB30100:IFVAL(TT$(4))<1THEN33500
33600 Y=5:A=2:O$="{black}Min Responses: {white}"+TT$(5)+"{ct c}":GOSUB30100:IFVAL(TT$(5))<1THEN33600
33700 O$="{black}Access: {white}ABCDEFGHIJKLMNOPQRSTUVWXYZ{ct k}      : {ct c}":GOSUB4
33800 O$=TT$(7)+"{ct c}":GOSUB4:C$=CHR$(20)+R$:GOSUB28:IFI=2THENO$=R$:GOSUB9:GOTO34200:ELSEDOUNTIL(LEN(TT$(7))>25):TT$(7)=TT$(7)+"0":LOOP
33900 FORX=1TO26:O$=CHR$(20):GOSUB9:NEXTX:O$="Toggle which: {ct c}":GOSUB4:Y=14:X=1:DO:POKE2831,1:GOSUB8:LOOPUNTILA$<>"":IFA$=R$THEN34100
34000 X=ASC(A$)-192:IFX>0ANDX<27THENY=15:O$=A$:GOSUB9:MID$(TT$(7),X,1)=MID$(STR$(ABS(VAL(MID$(TT$(7),X,1))-1)),2)
34100 FORX=1TOY:O$=CHR$(20):GOSUB9:NEXTX:GOTO33800
34200 Y=8:A=1:O$="{black}Subop flag   : {white}"+TT$(8)+"{ct c}":GOSUB30100:X=ASC(TT$(8))-192:IFX<1ORX>26THEN34200
34300 O$="{ct k}{black}A)bort, D)elete, S)ave, E)dit : {ct c}":GOSUB4:C$="ASED":GOSUB28:O$=A$+R$:GOSUB4:ONIGOTO940,34400,33100,34600
34400 O$="Saving...{ct c}":GOSUB4:OPEN2,U,2,CHR$(D)+":sys.msg bases":RECORD#2,M1:A$="":FORX=1TO8:A$=A$+TT$(X)+CHR$(13):NEXTX
34500 PRINT#2,LEFT$(A$,LEN(A$)-1):CLOSE2:FORI=1TO6:O$=CHR$(20):GOSUB9:NEXTI:O$="ed!":GOSUB4:GOTO940
34600 O$="{ct k}{black}Are you sure (y/n)?{ct c}":GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN34300:ELSETT$(6)="{pound}":TT$(7)="{pound}":GOTO34400
34700 GOSUB990:IFM3$="#"THEN34900:ELSEU1=ASC(M3$)-192:IFU1>0ANDU1<21THEN34900
34800 O$=T$(65):GOSUB4:X=1:GOSUB5:I=ASC(I$)-192:IFI<1ORI>20THEN920:ELSEU1=I
34900 U=D(1,0):D=D(1,1):OPEN2,U,2,CHR$(D)+":sys.u/d bases":RECORD#2,U1:FORX=1TO2:SYS8222:TT$(X)=MID$(STR$(VAL(I$)),2):NEXTX:FORX=3TO5:SYS8222:TT$(X)=I$:NEXTX
35000 FORX=6TO7:SYS8222:TT$(X)=MID$(STR$(VAL(I$)),2):NEXTX:CLOSE2:FORX=1TO7:MX(X)=LEN(TT$(X)):NEXTX
35100 Y=3:A=20:O$="{ct k}{black}Subboard Name: {white}"+TT$(3)+"{ct c}":GOSUB30100
35200 Y=1:A=2:O$="{black}Device number: {white}"+TT$(1)+"{ct c}":GOSUB30100
35300 Y=2:A=1:O$="{black}LU/Drive     : {white}"+TT$(2)+"{ct c}":GOSUB30100
35400 Y=6:A=4:O$="{black}Min blks     : {white}"+TT$(6)+"{ct c}":GOSUB30100:IFVAL(TT$(6))<1THEN35400
35500 Y=7:A=2:O$="{black}Credits/blk    {white}"+TT$(7)+"{ct c}":GOSUB30100:IFVAL(TT$(7))<1THEN35500
35600 O$="{black}Access : {white}ABCDEFGHIJKLMNOPQRSTUVWXYZ{ct k}       : {ct c}":GOSUB4
35700 O$=TT$(4)+"{ct c}":GOSUB4:C$=CHR$(20)+R$:GOSUB28:IFI=2THENO$=R$:GOSUB9:GOTO36100:ELSEDOUNTIL(LEN(TT$(4))>25):TT$(4)=TT$(4)+"0":LOOP
35800 FORX=1TO26:O$=CHR$(20):GOSUB9:NEXTX:O$="Toggle which: {ct c}":GOSUB4:Y=14:X=1:DO:POKE2831,1:GOSUB8:LOOPUNTILA$<>"":IFA$=R$THEN36000
35900 X=ASC(A$)-192:IFX>0ANDX<27THENY=15:O$=A$:GOSUB9:MID$(TT$(4),X,1)=MID$(STR$(ABS(VAL(MID$(TT$(4),X,1))-1)),2)
36000 FORX=1TOY:O$=CHR$(20):GOSUB9:NEXTX:GOTO35700
36100 Y=5:A=1:O$="{black}Subop flag   : {white}"+TT$(5)+"{ct c}":GOSUB30100:X=ASC(TT$(5))-192:IFX<1ORX>26THEN36100
36200 O$="{ct k}{black}A)bort, D)elete, S)ave, E)dit : {ct c}":GOSUB4:C$="ASED":GOSUB28:O$=A$+R$:GOSUB4:ONIGOTO940,36300,35100,36500
36300 O$="Saving...{ct c}":GOSUB4:OPEN2,U,2,CHR$(D)+":sys.u/d bases":RECORD#2,U1:A$="":FORX=1TO7:A$=A$+TT$(X)+CHR$(13):NEXTX
36400 PRINT#2,LEFT$(A$,LEN(A$)-1):CLOSE2:FORI=1TO6:O$=CHR$(20):GOSUB9:NEXTI:O$="ed!":GOSUB4:GOTO940
36500 O$="{ct k}{black}Are you sure (y/n)?{ct c}":GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN36200:ELSETT$(3)="{pound}":TT$(4)="{pound}":GOTO36300
36600 O$="{ct k}{black}ZOS interface v2.1":GOSUB4:GOSUB990:IFM3$="*"THENU=8:D=0:GOSUB990:GOTO36700:ELSEU=VAL(M3$):GOSUB990:D=VAL(M3$)
36700 O$="{ct k}{black}{ct n}"+MID$(STR$(U),2)+","+MID$(STR$(D),2)+">{ct c}":GOSUB4:GOSUB6:I=0:F$="":W1=0:IFI$="@"THENOPEN1,U,15:GOTO37200:ELSEIFI$=""THEN940
36800 A$=LEFT$(I$,1):IFLEN(I$)>3ANDMID$(I$,2,1)=":"THENF$=RIGHT$(I$,LEN(I$)-2)
36900 ONINSTR("#@we$%p^!d",A$)GOTO37300,37600,37900,38200,38400,38600,38800,39000,39100,40000
37000 IFINSTR(I$,",")ANDVAL(I$)>7ANDVAL(I$)<25THENU=VAL(I$):D=VAL(MID$(I$,INSTR(I$,",")+1)):O$="{black}Drive:"+STR$(U)+","+MID$(STR$(D),2):GOSUB4:GOTO36700
37100 OPEN1,U,15,I$
37200 INPUT#1,W1,I$,W2,W3:CLOSE1:O$="{black}"+MID$(STR$(W1),2)+", "+I$+","+MID$(STR$(W2),2)+","+MID$(STR$(W3),2):GOSUB4:GOTO36700
37300 IFI$="#"THEN36700:ELSEI$=RIGHT$(I$,LEN(I$)-1):IFINSTR(LEFT$(I$,3),"e")ANDINSTR(I$,":")<>INSTR(I$,"e")+1THEN36700
37400 I=VAL(LEFT$(I$,3)):IFIORLEFT$(I$,1)="0"THENW1=1
37500 DO:I$=RIGHT$(I$,LEN(I$)-1):LOOPUNTILVAL(I$)=0ANDLEFT$(I$,1)<>"0":GOTO36800
37600 IFF$=""THEN36700:ELSEOPEN2,U,2,STR$(D)+":"+F$+",s,r":IFI=0THEN37700:ELSEFORX=1TOI:SYS8213:NEXT
37700 C$="":SYS8210:IFPEEK(253)=0THENCLOSE2:GOTO36700
37800 O$="{ct k}{black}{ct n}S)kip next or (Return)>{ct c}":GOSUB4:C$="S"+R$:GOSUB28:O$=A$:GOSUB4:IFI=1THENSYS8213:GOTO37800:ELSE37700
37900 IFF$=""THEN36700:ELSEIFI=0THENO(12)=1:GOTO38100:ELSEOPEN2,U,2,STR$(D)+":"+F$+",s,r":FORX=1TOI:SYS8213:NEXTX:CLOSE2:IFPEEK(253)=0THENO(12)=2:GOTO38100
38000 O$="{ct k}{black}Not found - Append (y/n)?{ct c}":GOSUB4:C$="YN":GOSUB28:O$=A$:GOSUB4:IFI=2THEN36700:ELSEO(12)=0
38100 GOSUB1000:GOTO36700
38200 IFF$=""THEN36700:ELSEO(12)=2:O(10)=I:GOSUB8330
38300 IFO(11)THEN36700:ELSEGOSUB1000:GOTO36700
38400 IFI$="$"THENI$=I$+MID$(STR$(D),2)+":*"
38500 POKE253,0:OPEN2,U,0,I$:SYS8219:CLOSE2:O$=R$:GOSUB9:GOTO36700
38600 MID$(I$,INSTR(I$,"%"),1)="$":IFI$="$"THENI$=I$+MID$(STR$(D),2)+":*"
38700 POKE253,0:OPEN2,U,0,I$:SYS8228:CLOSE2:O$="{black}{ct n}"+MID$(STR$(PEEK(2858)),2)+" files counted.":GOSUB4:GOTO36700
38800 IFINSTR(I$,"e")THEN36700
38900 I$=RIGHT$(I$,LEN(I$)-1):IFW1=0THENO$=R$+STR$(PEEK(VAL(I$))):GOSUB4:GOTO36700:ELSEPOKEVAL(I$),I:GOTO36700
39000 IFF$=""THEN36700:ELSEI$=F$:W1=9999:GOTO23
39100 IFF$=""THEN36700:ELSEIFI>0THENOPEN2,U,2,STR$(D)+":"+F$:RECORD#2,I:GOTO39300:ELSEOPEN1,U,15:OPEN2,U,2,STR$(D)+":"+F$+",s,a":INPUT#1,E:IFE=0THEN39300
39200 CLOSE2:OPEN2,U,2,STR$(D)+":"+F$+",s,w"
39300 POKE2850,0:POKE2895,1:O$="{ct k}{ct n}{white}:{ct c}":GOSUB4:GOSUB6:A$="":IFI$=""THENCLOSE1:CLOSE2:GOTO36700
39400 IFI$=""THEN39900:ELSEX=INSTR(I$,"("):Y=INSTR(I$,")")
39500 IFX>1ANDY>X+1ANDY<X+5THENA$=A$+LEFT$(I$,X-1):I$=RIGHT$(I$,LEN(I$)-LEN(LEFT$(I$,X-1))):GOTO39400
39600 IFX=1ANDY>2ANDY<6THENIFVAL(MID$(I$,2,Y-2))>0THENA$=A$+CHR$(VAL(MID$(I$,2,Y-2))):I$=RIGHT$(I$,LEN(I$)-Y):GOTO39400
39700 IFXANDYTHENI=X:X=INSTR(MID$(I$,I+1),"("):IFX>ITHEN39500
39800 A$=A$+I$
39900 PRINT#2,A$:CLOSE2:CLOSE1:GOTO36700
40000 IFF$=""ORW1=0THEN36700:ELSEX=0:OPEN1,U,15,"s"+STR$(D)+":sys.work":OPEN2,U,2,STR$(D)+":"+F$+",s,r":INPUT#1,E:IFETHENCLOSE1:CLOSE2:GOTO36700
40100 OPEN3,U,3,STR$(D)+":sys.work,s,w":INPUT#1,E:IFETHEN40700
40200 IFX=ITHENSYS8213:IFPEEK(253)THEN40500
40300 IFX>0THENPRINT#3,CHR$(1)
40400 GOSUB31:X=X+1:IFPEEK(253)THEN40500:ELSE40200
40500 INPUT#1,E:CLOSE3:CLOSE2:IFE>0THEN40700:ELSEPRINT#1,"s"+STR$(D)+":"+F$:PRINT#1,"r"+STR$(D)+":"+F$+"=sys.work"
40600 OPEN2,U,2,STR$(D)+":"+F$+",s,r":GET#2,A$:E=ST:CLOSE2:IFE>0THENPRINT#1,"s"+STR$(D)+":"+F$:CLOSE1:GOTO36700:ELSECLOSE1:GOTO36700
40700 O$="Disk error, clearing...":GOSUB4:PRINT#1,"s"+STR$(D)+":sys.work":PRINT#1,"v":CLOSE1:GOTO36700
40800 O$="{ct k}{black}{ct k}Enter time {white}(HH:MM format): {ct c}":GOSUB4:X=5:GOSUB5:IFLEN(I$)<5THEN940:ELSEO$="{ct k}{black}AM or PM {white}(A/P): {ct c}":GOSUB4:C$="AP":GOSUB28:O$=A$:GOSUB4
40900 I=VAL(LEFT$(I$,2)):IFI=12ANDA$="A"THENA$="P":ELSEIFI=12THEN41100
41000 IFA$="P"THENI=I+80
41100 POKE56587,DEC(STR$(I)):POKE56586,DEC(RIGHT$(I$,2)):POKE56585,1:POKE56584,1:I=PEEK(56584)
41200 O$="{ct k}{black}Enter date {white}(MM/DD/YY format): {ct c}":GOSUB4:X=8:GOSUB5:IFLEN(I$)<8THEN940:ELSEPOKE2879,VAL(LEFT$(I$,2)):POKE2880,VAL(MID$(I$,4,2))
41300 POKE2863,VAL(RIGHT$(I$,2)):GOSUB400:O$="{ct k}{black}Now: {white}"+O$(3):GOTO940
41400 O$=T$(51):GOSUB4:X=0
41500 DO:X=X+1:LOOPUNTILUI$(X)="^"ORUI$(X)=""ORX>M(14):IFX>M(14)THENO$=T$(43):GOSUB4:GOTO920
41600 A=X:W1=402:GOTO30500
55555 OPEN1,8,15,"s0:prg.sysop":CLOSE1:DSAVE"prg.sysop":END
