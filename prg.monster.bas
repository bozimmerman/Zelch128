!--------------------------------------------------
!- Monday, May 15, 2017 3:02:37 AM
!- Import of : 
!- c:\src\zelch128\prg.monster.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
30000 SYS15625:REM =========== AUTO-MAINTENANCE W/SCRIPT =========== 06/10/92 =
30010 IFPEEK(2961)<2THENPOKE2829,1:POKE2823,1:POKE2961,2:GOSUB18:PRINT"{clear}{down}{ct n}{cyan}Monster Mode v2.0--Loading script...";:X=-1:ELSE30210
30020 POKE2827,0:POKE2825,255:POKE2824,1:O$="Monster Mode v2.0":GOSUB250:P0=1
30030 OPEN1,D(1,0),15:OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.script,s,r":INPUT#1,A:IFATHENPRINT"{left*17}Default script.  ":CLOSE1:CLOSE2:GOTO30050
30040 X=X+1:SYS8222:TT$(X)=I$:IFPEEK(253)=0THEN30040:ELSETT$(X+1)="{pound}":CLOSE2:CLOSE1:PRINT:GOTO30110
30050 TT$(0)="user 60":TT$(1)="msg":TT$(2)="news":TT$(3)="collect":TT$(4)="{pound}":X=3:GOTO30110
30060 OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.network":RETURN
30100 REM ----------- CHANGE COMMANDS TO LOWERCASE ----------------------------
30110 FORI=0TOX:IFTT$(I)=" "ORTT$(I)=""ORLEFT$(TT$(I),1)=";"THENTT$(I)="^"
30120 NEXTI:FORI=0TOX:A=0:FORY=1TOLEN(TT$(I)):A$=MID$(TT$(I),Y,1)
30130 IFA$="{f5}"THENA=ABS(A-1):GOTO30160
30140 IFATHEN30160
30150 MID$(TT$(I),Y,1)=CHR$(ASC(A$)AND127)
30160 NEXTY:NEXTI
30200 REM ----------- EXECUTE NEXT SCRIPT COMMAND -----------------------------
30210 CLOSE1:CLOSE15:CLOSE2:CLOSE3:CLOSE4:X=-1
30215 X=X+1:IFX>100THEN50010
30220 A$=TT$(X):IFA$="{pound}"THEN50010:ELSEIFA$="^"THEN30215
30230 TT$(X)="^":IFINSTR(A$,"{f5}")=0THENF$="":GOTO30250
30240 F$=MID$(A$,INSTR(A$,"{f5}")+1)+"{f5}":F$=LEFT$(F$,INSTR(F$,"{f5}")-1)
30250 A=INSTR(A$,","):IFA=0THENU=D(0,0):D=D(0,1):GOTO30270
30260 U=VAL(MID$(A$,A-2)):D=VAL(MID$(A$,A+1))
30270 A=INSTR(A$,"{f5}"):IFA<1THENA=INSTR(A$,","):IFA<1THENI$=A$:GOTO30300
30280 I$=LEFT$(A$,A-1)
30300 IFINSTR(I$,"user")THEN30610
30310 IFINSTR(I$,"msg")ORINSTR(I$,"message")THEN31010
30320 IFINSTR(I$,"news")THEN30810
30330 IFINSTR(I$,"collect")ORINSTR(I$,"validate")THEN31210
30340 IFINSTR(I$,"print")THENO$=F$:GOSUB4:GOTO30210
30350 IFINSTR(I$,"run")THENI$=F$:O$="Online Basic":GOSUB250:POKE2961,2:GOTO8940
30360 IFINSTR(I$,"execute")THENO$="Zelch Specific Module":GOSUB250:I$=F$:GOTO23
30370 IFINSTR(I$,"dos")ORINSTR(I$,"disk")THEN31310
30380 IFINSTR(I$,"display")ORINSTR(I$,"file")THENGOSUB26:GOTO30210
30390 IFINSTR(I$,"hard")THEN31410
30400 IFINSTR(I$,"clear")ORINSTR(I$,"reset")THEN31510
30410 IFINSTR(I$,"modem")ORINSTR(I$,"hayes")THENA$=F$:GOSUB2:GOTO30210
30420 IFINSTR(I$,"network")THEN32000
30430 IFINSTR(I$,"unpack")THEN35100
30590 GOTO30210
30600 REM ----------- USER WEED ROUTINES --------------------------------------
30610 IFINSTR(A$," ")=0THENA$=A$+" 60"
30620 A$=MID$(A$,INSTR(A$," ")+1):X=VAL(A$):IFX<1THEN30610
30630 GOSUB400:OPEN1,D(5,0),15:OPEN2,D(4,0),2,MID$(STR$(D(4,1)),2)+":sys.userfiles":W1=1:W5=0
30640 W2=VAL(MID$(O$(2),5,2)):W3=VAL(MID$(O$(2),3,2)):W4=VAL(LEFT$(O$(2),2)):FORI=1TOX
30650 W2=W2-1:IFW2<1THENW3=W3-1:W2=PEEK(2880+W3):IFW3<1THENW3=12:W2=31:W4=W4-1:IFW4<0THENW4=99
30660 NEXTI:W1$=RIGHT$(STR$(W4),2)+RIGHT$(STR$(W3),2)+RIGHT$(STR$(W2),2):DO:W7=INSTR(W1$," "):IFW7>0THENMID$(W1$,W7,1)="0"
30670 LOOPUNTILW7=0
30680 O$="Checking userfiles...":GOSUB250:I=FRE(1)
30690 IFUI$(W1)="^"THENW1=W1+1:IFW1<501THEN30690
30700 IFUI$(W1)=""ORUI$(W1)="{pound}"ORW1>500THENCLOSE2:CLOSE1:GOTO30210
30710 RECORD#2,W1:FORI=1TO6:SYS8222:NEXTI:A$=I$:FORY=1TO4:GET#2,W9$:NEXTY:SYS8222:IFLEFT$(A$,6)>W1$ORMID$(I$,3,1)="1"THENW1=W1+1:GOTO30690
30720 O$="Munching user":GOSUB250:RECORD#2,W1:A$="0"+R$:PRINT#2,"^"+R$+"{f4}"+R$+"^"+R$+"xxx-xxxx"+R$+"A"+R$+"^"+R$+"{right*4}0000000"+R$+A$+"{pi}{pi}{pi}{pi}"+A$+A$+A$+A$+A$;
30730 PRINT#1,"s"+MID$(STR$(D(5,1)),2)+":mail."+STR$(W1):P0$(P0)="Munched user #"+STR$(W1)+"/"+UI$(W1):UI$(W1)="^":P0=P0+1:W1=W1+1:GOTO30680
30800 REM ----------- NEWS FILE ROUTINES --------------------------------------
30810 IFF$=""THENF$="sys.news":U=D(1,0):D=D(1,1)
30820 O$="Updating "+F$:GOSUB250:X=0:GOSUB400:OPEN1,U,15:OPEN2,U,2,MID$(STR$(D),2)+":"+F$+",s,r":INPUT#1,E:IFE>0THEN30910
30830 SYS8222:SYS8222:IFPEEK(253)THEN30210
30840 IFI$>=O$(2)THENSYS8213:GOTO30830
30850 CLOSE2:CLOSE1:OPEN1,U,15,"s"+MID$(STR$(D),2)+":sys.work":OPEN2,U,2,MID$(STR$(D),2)+":"+F$+",s,r":X=0
30860 OPEN3,U,3,MID$(STR$(D),2)+":sys.work,s,w"
30870 SYS8222:A$=I$:SYS8222:IFPEEK(253)THEN30910
30880 IFO$(2)>I$THENSYS8222:SYS8213:P0$(P0)="Stomped "+F$+" #"+STR$(X+1)+"/{f5}"+I$+"{f5}":P0=P0+1:GOTO30870
30890 IFX>0THENPRINT#3,CHR$(1)
30900 PRINT#3,A$:PRINT#3,I$:GOSUB31:X=X+1:GOTO30870
30910 CLOSE2:CLOSE3:IFX=0THENPRINT#1,"s"+MID$(STR$(D),2)+":sys.work":PRINT#1,"s"+MID$(STR$(D),2)+":"+F$:GOTO30210
30920 PRINT#1,"s"+MID$(STR$(D),2)+":"+F$:PRINT#1,"r"+MID$(STR$(D),2)+":"+F$+"=sys.work":GOTO30210
31000 REM ----------- WEED MESSAGE BASES --------------------------------------
31010 W1=1
31020 IFW1>26THEN30210:ELSECLOSE2:OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.msg bases":RECORD#2,W1:SYS8222:U=VAL(I$):IFU<8THENW1=W1+1:GOTO31020
31030 INPUT#2,D:FORI=1TO5:SYS8222:TT$(150+I)=I$:NEXTI:CLOSE2:I=FRE(1)
31040 O$="Sniffing sig "+CHR$(192+W1):GOSUB250
31050 IFVAL(TT$(155))=0THENW1=W1+1:GOTO31020
31060 OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.msg data":RECORD#2,((W1-1)*51)+51:INPUT#2,W2:FORI=1TOW2:RECORD#2,((W1-1)*51)+I:SYS8222:MN$(I)=I$:INPUT#2,MR(I)
31070 NEXTI:CLOSE2:FORI=1TOW2:IFMR(I)=0ORVAL(MN$(I))=0ORMR(I)<VAL(TT$(152))THEN31160
31080 O$="Chewing msg "+CHR$(192+W1)+" #"+STR$(I):GOSUB250:CLOSE1:CLOSE2:CLOSE3:OPEN1,U,15,"s"+MID$(STR$(D),2)+":sys.work"
31090 OPEN2,U,2,MID$(STR$(D),2)+":msg."+CHR$(W1+64)+STR$(I)+",s,r"
31100 OPEN3,U,3,MID$(STR$(D),2)+":sys.work,s,w":P0$(P0)="Chewed message "+CHR$(192+W1)+" #"+STR$(I):P0=P0+1
31110 SYS8222:IFI$<>CHR$(1)THENPRINT#3,I$:GOTO31110
31120 PRINT#3,I$:FORX=1TOMR(I)-VAL(TT$(153)):SYS8213:MR(I)=MR(I)-1:NEXTX
31130 SYS8222:IFPEEK(253)=0THENPRINT#3,I$:GOTO31130
31140 PRINT#3,I$:CLOSE3:CLOSE2:PRINT#1,"s"+MID$(STR$(D),2)+":msg."+CHR$(64+W1)+STR$(I):PRINT#1,"r"+MID$(STR$(D),2)+":msg."+CHR$(64+W1)+STR$(I)+"=sys.work"
31150 CLOSE1:OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.msg data":RECORD#2,((W1-1)*51)+I:PRINT#2,MN$(I)+CHR$(13)+STR$(MR(I)):CLOSE2:O$="Burp!":GOSUB250
31160 NEXTI:W1=W1+1:GOTO31020
31200 REM ----------- COLLECT/VALIDATE ----------------------------------------
31210 O$="Eating Disk"+STR$(U)+","+MID$(STR$(D),2):GOSUB250
31220 OPEN1,U,15,"v"+MID$(STR$(D),2):GOTO30210
31300 REM ----------- DOS COMMANDS --------------------------------------------
31310 O$="Drooling on drive"+STR$(U)+","+MID$(STR$(D),2):GOSUB250
31320 OPEN1,U,15,F$:INPUT#1,I:CLOSE1:GOTO30210
31400 REM ----------- HARDCOPY A SEQ FILE TO PRINTER DEVICE 4 -----------------
31410 O$="Scratching on paper":GOSUB250
31420 OPEN1,U,15:OPEN2,U,2,MID$(STR$(D),2)+":"+F$+",s,r":INPUT#1,E:IFETHENCLOSE1:CLOSE2:GOTO30210:ELSEOPEN4,4,7
31430 SYS8222:FORX=1TO4:DO:A$=MID$("{ct k}{f6}{f5}{f7}",X,1):A=INSTR(I$,A$):IFATHENMID$(I$,A,1)=MID$(CHR$(13)+CHR$(13)+CHR$(34)+" ",X,1)
31440 LOOPUNTILA=0:NEXTX:PRINT#4,I$:IFPEEK(253)THENCLOSE1:CLOSE2:CLOSE4:GOTO30210:ELSE31430
31500 REM ----------- CLEAR OR RESET SYSTEM VARIABLE --------------------------
31510 A=INSTR(A$," "):IFA=0THEN30210
31520 A$=MID$(A$,A+1):IFVAL(A$)<1THEN31510
31530 O(VAL(A$))=0:GOTO30210
32000 REM ----------- NETWORK CALL ROUTINES -----------------------------------
32010 M5=0:O(19)=0:BLOAD"p-xfer 1300",U(D(0,0)),D(D(0,1)):O$(23)="Punter":A$="e0":GOSUB2
32015 GOSUB30060:RECORD#2,27:FORI=6TO10:SYS8222:MN$(I)=I$:NEXTI:CLOSE2
32020 GOSUB30060
32025 M5=M5+1:RECORD#2,M5:FORI=1TO5:SYS8222:MN$(I)=I$:NEXTI:IFM5>26ORMN$(1)="{pound}"THEN30210
32030 IFMN$(4)<>MN$(3)ORMN$(1)="^"THEN32025:ELSEW9$=MN$(4):IFLEFT$(MN$(4),1)="L"THENW9$=RIGHT$(MN$(4),7)
32040 CLOSE2:OPEN1,D(5,0),15:OPEN2,D(5,0),2,MID$(STR$(D(5,1)),2)+":net."+MID$(MN$(4),2)+",s,r":INPUT#1,E:CLOSE1:CLOSE2:IFETHEN32020
32050 O$="Snarling with "+W9$:GOSUB250
32100 Y=0:I=VAL(MN$(5))*300:IFI<>BATHENGOSUB450:Y=0
32130 PRINT#5,R$R$:SLEEP2:TI$="000000"
32140 GET#5,A$:IFA$<>""THEN32140
32150 PRINT#5,"atdt"+W9$:I$=""
32160 GET#5,A$:IFVAL(TI$)>30THENI$="6":GOTO32190
32170 IFA$=""THEN32160
32180 IFA$<>CHR$(13)THENI$=I$+A$:GOTO32160
32190 IFI$=""THEN32160:ELSEA$="":FORI=1TOLEN(I$):W2=ASC(MID$(I$,I,1))AND127:IFW2>47ANDW2<58THENA$=A$+CHR$(W2)
32200 NEXTI:I$=A$:IFI$=""THEN32160
32220 A=VAL(I$):IFA=1ORA=5ORA=10THEN32250
32230 IFINSTR(I$,"dt")THENI$="":GOTO32160
32240 IFY=0THENY=1:GOTO32130:ELSEGOSUB13:GOTO32020
32250 SLEEP7:DO:GET#5,A$:LOOPUNTILA$="":PRINT#5,"ZZZZZ";:GOSUB35000:IFE>1THENGOSUB13:GOTO32020
32260 SLEEP2:FORI=1TOLEN(MN$(7)):PRINT#5,MID$(MN$(7),I,1);:FORX=1TO50:NEXTX:NEXTI:PRINT#5,R$;:GOSUB35000:ONEGOTO32270,32260,32020
32270 P0$(P0)="Snarled with "+W9$:P0=P0+1:U=D(5,0):D=D(5,1):F$="net."+MID$(MN$(4),2):I=2:GOSUB550:OPEN1,U,15,"s"+MID$(STR$(D),2)+":"+F$:CLOSE1:GOSUB35000
32280 IFE>1THENGOSUB13:GOTO32020:ELSEF$="net."+MID$(MN$(8),2)
32285 GOSUB590:SYS4885:SYS4873:IFPEEK(512)=1THENGOSUB13:GOTO32020
32290 OPEN1,U,15:OPEN2,U,2,MID$(STR$(D),2)+":"+F$+",s,a":PRINT#2,"{ct a}":INPUT#1,E:IFETHENCLOSE2:OPEN2,U,2,MID$(STR$(D),2)+":"+F$+",s,w"
32300 SLEEP1:SYS59074:SYS4885:SYS4867:SLEEP1:POKE2585,PEEK(2584):CLOSE2
32310 CLOSE1:GOSUB13:IFPEEK(215)THENFAST:GOTO32020:ELSE32020
35000 TI$="000000":E=0:DO:GET#5,A$:IFA$<>""THENA$=CHR$(ASC(A$)OR128)
35010 E=INSTR("GB",A$):LOOPUNTIL(E)ORVAL(TI$)>30:IFETHENDO:GET#5,A$:LOOPUNTILA$="":RETURN:ELSEE=3:GOTO13
35100 REM ----------- UNPACK NETWORK FILES ------------------------------------
35110 OPEN2,D(1,0),2,MID$(STR$(D(1,1)),2)+":sys.network":FORX=1TO27:RECORD#2,X:FORI=0TO4:INPUT#2,M$(X,I):NEXTI:NEXTX:CLOSE2
35115 GOSUB400:IFD(5,0)<>D(6,0)THENOPEN15,D(6,0),15
35120 OPEN1,D(5,0),15:OPEN2,D(5,0),2,MID$(STR$(D(5,1)),2)+":net."+MID$(M$(27,2),2)+",s,r":INPUT#1,E:IFETHENCLOSE1:CLOSE2:CLOSE15:GOTO30210
35125 O$="Hurling files about...":GOSUB250
35130 SYS8222:W1$=I$:SYS8222:W2$=I$:SYS8222:W3$=I$:IFW1$<>MID$(M$(27,2),2)THEN35500
35140 ONVAL(W3$)GOTO35150,35170
35150 W3$=MID$(W3$,2):X=0:DO:X=X+1:LOOPUNTILUI$(X)=W3$ORX>499ORUI$(X)="":IFUI$(X)<>W3$THEN35530
35160 U=D(5,0):D=D(5,1):F$="mail."+STR$(X):O$(5)="#"+W2$:GOSUB36000:GOTO35130
35170 U=D(6,0):D=D(6,1):F$="sys.feedback":O$(5)="#"+W2$:O(2)=O(2)+1:GOSUB36000:GOTO35130
35500 IFPEEK(253)THENCLOSE2:PRINT#1,"s"+MID$(STR$(D(5,1)),2)+":net."+MID$(M$(27,2),2):GOTO30210
35510 X=0:DO:X=X+1:LOOPUNTILMID$(M$(X,2),2)=W1$ORX>26:IFX>26THENSYS8213:GOTO35130
35520 O$(5)=W1$+R$+W2$+R$+W3$:F$="net."+MID$(M$(X,3),2):U=D(5,0):D=D(5,1):GOSUB36000:GOTO35130
35530 U=D(5,0):D=D(5,1):SYS8222:X=0:DO:X=X+1:LOOPUNTILMID$(M$(X,2),2)=W2$ORX>26:IFX>26THENSYS8213:GOTO35130
35540 F$="net."+MID$(M$(X,3),2):OPEN3,U,3,MID$(STR$(D),2)+":"+F$+",s,w":INPUT#1,E:IFETHENCLOSE3:OPEN3,U,3,MID$(STR$(D),2)+":"+F$+",s,a":PRINT#3,"{ct a}"
35550 PRINT#3,W2$+R$+W1$+R$+"1"+I$+R$+"ZELCH MONSTER"+R$+" 1"+R$+O$(3)+R$+R$+"{black}{ct k}Sorry, {f5}"+W3$+"{f5} was not{f7}found at "+W1$+".":CLOSE3:SYS8213:GOTO35130
36000 OPEN3,U,3,MID$(STR$(D),2)+":"+F$+",s,w":INPUT#1,E:IFU<>D(5,0)THENINPUT#15,E
36005 IFETHENCLOSE3:OPEN3,U,3,MID$(STR$(D),2)+":"+F$+",s,a":PRINT#3,CHR$(1)
36010 PRINT#3,O$(5):GOSUB31:CLOSE3:RETURN
50000 REM ----------- MONSTER MODE REPORT -------------------------------------
50010 U=D(1,0):D=D(1,1):OPEN1,U,15:OPEN2,U,2,MID$(STR$(D),2)+":sys.userlog,s,a":INPUT#1,E:IFE>0THENCLOSE2:OPEN2,U,2,MID$(STR$(D),2)+":sys.userlog,s,w"
50020 PRINT#2,"{reverse on}{light green}MONSTER{sh space}MODE{sh space}REPORT{reverse off}: ";:IFP0=1THENPRINT#2,"Growl!{ct k}":CLOSE2:CLOSE1:GOTO50040
50030 PRINT#2:FORI=1TOP0-1:PRINT#2,P0$(I):P0$(I)="":NEXTI:PRINT#2,"":CLOSE2:CLOSE1
50040 FORI=1TO50:MN$(I)="":MR(I)=0:NEXTI:FORI=0TO200:TT$(I)="":NEXTI:POKE2961,0:I=FRE(1):I$="prg.logon":U=D(0,0):D=D(0,1):GOTO23
55555 U=PEEK(186):OPEN1,U,15,"s0:prg.monster":CLOSE1:DSAVE"prg.monster",U(U)
