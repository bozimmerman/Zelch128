!--------------------------------------------------
!- Saturday, May 20, 2017 1:36:31 AM
!- Import of : 
!- c:\src\zelch128\extras\gam.wheel.prg
!- Unknown Machine
!--------------------------------------------------
20 SZ=RND(-TI/37)
30 DIMPR$(20),PU$(200),LP(200),WH$(200),ZP$(50),ZP(50)
40 PR$(1)="300":PR$(2)="150":PR$(3)="1000":PR$(4)="500":PR$(5)=PR$(1)
50 PR$(6)="bankrupt":PR$(7)="lose money":PR$(8)="freespin":PR$(9)="550"
60 PR$(10)="250":PR$(11)="350":PR$(12)="750"
70 PRINT"{blue}"
80 PRINT "{blue}{clear}{down*5}          Wheel Of Fortune!"
90 PRINT"{down}          By: Bo Zimmerman"
100 PRINT"         Completed: 05/06/88"
110 PRINT "{light blue}{down*3}        Setting Up Puzzles...";
120 RESTORE
130 READPU$(PL),WH$(PL):IFPU$(PL)="00"THENPL=PL-1:GOTO150
140 PL=PL+1:GOTO130
150 READZP$(NB),ZP(NB):IFZP$(NB)="**"THENNB=NB-1:GOTO170
160 NB=NB+1:GOTO 150
170 PRINT"{down}":PZ=INT(RND(TI)*PL)+1
180 PZ$=PU$(PZ)
190 FORI=1TOLEN(PZ$)
200 LP(I)=0
210 NEXT
220 FORI=1TOLEN(PZ$)
230 IFMID$(PZ$,I,1)=CHR$(32)THENLP(I)=1
240 NEXT:PRINT"{down*17}{clear}"
1000 REM ************** MAIN ROUTINE ************************
1010 FORI=1TO2000:NEXT:PRINT"{clear}{down}{pink}{down*2}Puzzle type: ";
1020 IFWH$(PZ)="pe"THENPRINT"PERSON OR{sh space}PEOPLE"
1030 IFWH$(PZ)="ev"THENPRINT"EVENT"
1040 IFWH$(PZ)="th"THENPRINT"THING{sh space}OR{sh space}THINGS"
1050 IFWH$(PZ)="ph"THENPRINT"PHRASE"
1060 IFWH$(PZ)="pl"THENPRINT"PLACE"
1070 IFWH$(PZ)="ti"THENPRINT"TITLE"
1080 IFWH$(PZ)="oc"THENPRINT"OCCUPATION"
1090 IFWH$(PZ)="qu"THENPRINT"QUOTATION"
1100 IFWH$(PZ)="fc"THENPRINT"FICTIONAL CHARACTER"
1110 IFWH$(PZ)="la"THENPRINT"LANDMARK"
1120 PRINT"{yellow}":FORI=1TOLEN(PZ$):LQ$=MID$(PZ$,I,1)
1140 IFLP(I)=1THENPRINTLQ$;:GOTO1160
1150 IFLP(I)=0THENPRINT"-";
1160 NEXTI:CN=0
1170 PRINTCHR$(13)+"{pink}Money:{yellow}";MN;"{left}   {pink}Free spins:{yellow}";SN;"{left}       "
1180 PRINT"{pink} Used:{yellow} ";LU$
1190 INPUT"{down}{red}(S)pin, (V)owel, (G)uess, (Q)uit";CH$
1200 IFCH$="q"ORCH$="Q"THENEND
1210 IFCH$="g"THENPRINT"{down}{purple}Guess the puzzle:":GOTO1550
1220 IFCH$="v"THENPRINT"{down}{purple}Buy a vowel:":GOTO1360
1230 IFCH$<>"s"THEN1190
1240 PRINT"{purple}{down}Spin the wheel: ";:FORRO=1TO10
1250 RT=INT(RND(1)*12)+1:PRINTPR$(RT);:FORI=1TOLEN(PR$(RT)):PRINTCHR$(20);:NEXT
1260 FORW=1TO50:NEXTW:NEXTRO:PRINTPR$(RT)
1270 IFPR$(RT)="freespin"THENSN=SN+1:FORI=1TO2000:NEXTI:GOTO1000
1280 IFPR$(RT)="bankrupt"ANDSN<1ANDMN>0THENMN=0:GOTO1000
1281 IFPR$(RT)="bankrupt"ANDSN>0THENSN=SN-1:GOTO1000
1282 IFPR$(RT)="bankrupt"THENMN=MN-500:GOTO1000
1290 IFPR$(RT)="lose money"ANDSN<1THENMN=MN-250:GOTO1000
1300 IFPR$(RT)="lose money"ANDSN>0THENSN=SN-1:GOTO1000
1310 INPUT"{down}{cyan}Enter a consonant";LT$:IFLEN(LT$)>1THEN1310
1320 FL=0:FORI=1TOLEN(LU$):IFLT$=MID$(LU$,I,1)THENFL=1
1330 NEXT:IFFL=0THENLU$=LU$+LT$
1335 IFFL=1THEN1000
1340 LT=ASC(LT$):IFLT<66ORLT>90ORLT=69ORLT=73ORLT=79ORLT=85THEN1310
1350 GOTO1490
1360 IFMN>=250THEN1390
1370 PRINT"{down}{light blue}You don't have enough money!":FORU=1TO2000:NEXT:GOTO1000
1390 MN=MN-250
1400 INPUT"{light blue}Vowel";LT$:IFLT$="a"ORLT$="e"ORLT$="i"ORLT$="o"ORLT$="u"THENGOTO1420
1410 GOTO1400
1420 FL=0:FORI=1TOLEN(LU$):IFLT$=MID$(LU$,I,1)THENFL=1
1430 NEXT:IFFL=0THENLU$=LU$+LT$
1440 PO=0:FORI=1TOLEN(PZ$)
1450 IFMID$(PZ$,I,1)=LT$THENLP(I)=1:PO=PO+1
1460 NEXT
1470 IFPO>0THENPRINT"{down}{light blue}There are"+STR$(PO)+" "+LT$+"'s.":GOTO1000
1480 PRINT"{down}{blue}Sorry...":FORI=1TO2000:NEXT:GOTO1000
1490 PO=0:FORI=1TOLEN(PZ$)
1500 IFMID$(PZ$,I,1)=LT$THENLP(I)=1:PO=PO+1:MN=MN+VAL(PR$(RT))
1510 NEXT
1520 IFPO>0THENPRINT"{down}{light blue}There are"+STR$(PO)+" "+LT$+"'s":GOTO1000
1530 PRINT"{down}{white}Sorry...":FORI=1TO2000:NEXT:MN=MN-VAL(PR$(RT))
1540 GOTO1000
1550 INPUT"{down*2}{light blue}Go ahead";TR$:IFTR$<>PZ$THEN1000
1560 PRINT"{down}{blue}YOU{sh space}DID{sh space}IT!!!"
1570 IFMN>0THENGOSUB1650
1610 IFMN>=0THENPRINT"{light green} Your total cash winnings were $";MN
1620 IFMN<0THENPRINT" You owe the wheel of fortune $";ABS(MN)
1630 PRINT"{down*4} bye bye..."
1640 END
1650 PRINT"{down}":FORI=1TONB:PRINT"{light gray}";CHR$(I+64);") ";ZP$(I);TAB(20);"- $";ZP(I)
1660 NEXT
1670 PP=0:PRINT"{down*3}{light green}Choose item or '*' to put":PRINT"the rest on a gift certificate!"
1680 PRINT"MONEY : $";MN"{left} "
1690 FORI=1TONB:IFZP(I)<MNTHENPP=1
1700 NEXT:IFPP=0THENPRINT"{red}Can't buy anything";:FORI=0TO2000:NEXT:GOTO1770
1710 GETA$:IFA$=""THEN1710
1720 IFA$="*"THEN1770
1730 IFASC(A$)<65ORASC(A$)>(65+NB)THEN1710
1740 A=ASC(A$):A=A-64:IFZP(A)>MNTHEN1710
1750 MN=MN-ZP(A):PRINT "{down}{green}You just bought a brand new":PRINTZP$(A)
1760 FORI=0TO3000:NEXT:GOTO1650
1770 PRINT"{down*2}{green}You got many wonderous prizes today!{down}":RETURN
1780 DATA SUNDAY NEWSPAPER,TH,HIGH SCHOOL SWEETHEART,PE
1790 DATA THE HOPE DIAMOND,TH,ALL DRESSED UP AND NO PLACE TO GO,PH
1800 DATA DIAHANN CAROL,PE,SILVER WEDDING ANNIVERSARY,EV
1810 DATA HOT SPRINGS ARKANSAS,PL,A STITCH IN TIME SAVES NINE,PH
1820 DATA MICHAEL LANDON,PE,SWEAT SHIRT,TH,OFF THE TOP OF MY HEAD,PH
1830 DATA LEMON MARANGUE PIE,TH,LEAVE IT TO BEAVER,TI
1840 DATA DOLLARS AND CENTS,TH,THE LUCK OF THE IRISH,PH
1850 DATA CHOCOLATE PUDDING,TH,SURPRISE BIRTHDAY PARTY,EV
1860 DATA DENVER COLORADO,PL,HOPE SPRINGS ETERNAL,QU,FAMOUS LAST WORDS
1870 DATA PH,THE AGONY AND THE ECSTASY,TI,IN MORE WAYS THAN ONE,PH
1880 DATA VOLLEYBALL TEAM,PE,PEACE OFFERING,TH,JOHN WAYNE,PE
1890 DATA NINE TIMES OUT OF TEN,PH,LET THE CAT OUT OF THE BAG,PH
1900 DATA SENATOR BARRY GOLDWATER,PE,CASE DISMISSED,PH,SOCCER BALL,TH
1910 DATA LOOK BEFORE YOU LEAP,PH,COMPUTER SOFTWARE,TH
1920 DATA SPLIT MY SIDES LAUGHING,PH,MEAT AND POTATOES,TH
1930 DATA FOOTBALL STADIUM,PL,DYNASTY,TI,THE BIG BAD WOLF,FC
1940 DATA LIFE IS A CABARET OLD CHUM,QU,CALM COOL AND COLLECTED,PH
1950 DATA ARTE JOHNSON,PE,WILD GOOSE CHASE,PH,CASHMERE SWEATER,TH
1960 DATA AS THE CROW FLIES,PH,COME UP AND SEE ME SOMETIME,QU
1970 DATA TICKET STUB,TH,NOT A MOMENT TOO SOON,PH,SUPER BOWL,EV
1980 DATA SENSE OF HUMOR,PH,THE LOVE BOAT,TI,SPLIT SECOND,PH
1990 DATA A LEGEND IN HIS OWN TIME,PH,NEW ORLEANS,PL,PROGRAMMER,OC
2000 DATA PLANET ZELCH,PL,KISS,PE,DALLAS COWBOYS,PE,THE ZACMAN,FC
2010 DATA THE TOWER OF LONDON,LA,THE PIRATES OF PENZANCE,TI
2020 DATA ROOM TEMPERATURE,PH,MICHELE LEEX,PE,THE SEVEN HILLS OF ROME,PL
2030 DATA DO YOU KNOW THE WAY TO SAN JOSE,TI,CIRCUS OF THE STARS,EV
2040 DATA SATURDAY NIGHT LIVE,TI,HAVING THE TIME OF MY LIFE,PH
2050 DATA SALT LAKE CITY UTAH,PL,MORSE CODE,TH,ANGELA LANDSBURY,PE
2060 DATA ROOM AND BOARD,PH,THE LAST DAYS OF POMPEII,TI
2070 DATA TRIAL LAWYER,OC,TAMPA BAY BUCCANEERS,PE,SHERWOOD FOREST,PL
2080 DATA SOMEBODY UP THERE LIKES ME,QU,THEY LIVED HAPPILY EVER AFTER,QU
2090 DATA WALTER CRONKITE,PE,TEAM HORSES,TH,EXTENSION PHONE,TH
2100 DATA MY FUNNY VALENTINE,TI,LIFETIME GUARANTEE,TH,INCREDIBLE HULK,FC
2110 DATA LLOYD BRIDGES,PE,LOS ANGELES LAKERS,PE,WHEEL OF FORTUNE,TI
2120 DATA DON RICKLES,PE,SPEED CHECKED BY RADAR,PH,ONE FROM THE HEART,PH
2130 DATA I ONLY HAVE EYES FOR YOU,QU,CONFLICT OF INTEREST,PH
2140 DATA LONG JOHN SILVER,FC,DELUSIONS OF GRANDEUR,PH,ZELCH BE COOL,QU
2150 DATA WOMEN AND CHILDREN FIRST,PH,HELP,QU,DEATH,EV,THE CIVIL WAR,EV
2160 DATA 00,00
2170 DATA UMBRELLA TABLE,119,STRUCTO GAS GRILL,219,ROPE HAMMACK,69
2180 DATA GOLD WATCH,117,MINOLTA CAMARA,129,UGLY PAINTING,2000
2190 DATA COLOR TV,300,GENERIC COMPUTER,1500,CERAMIC DALMATION,400
2200 DATA REFRIGERATOR,579,12MOS FRUIT CAKES,50,HIS AND HER BIKES,379
2210 DATA TRIP TO ETHIOPIA,3
2220 DATA"**",99
