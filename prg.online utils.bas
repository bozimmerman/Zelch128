!--------------------------------------------------
!- Monday, May 15, 2017 3:03:33 AM
!- Import of : 
!- c:\src\zelch128\prg.online utils.prg
!- Commodore 128 BASIC 7/7.1
!--------------------------------------------------
30000 REM ====================== ONLINE SYSOP MENU V1.0 ============ 06/30/91 =
30010 FORI=1TO4:CLOSE(I):NEXTI:W2=U:W3=D:W4=PEEK(2974):POKE2974,0:WINDOW0,4,PEEK(238),23,1:W6=PEEK(2825):W5=PEEK(2823):POKE2823,1:FORX=1TO6:READTT$(X)
30020 IFPEEK(238)=39ANDLEN(TT$(X))<13THENTT$(X)=TT$(X)+" ":GOTO30020
30030 NEXTX:C=1
30035 W7=O(19):IFW7<>2THENBLOAD"l-xfer 1300",U(D(0,0)),D(D(0,1)):O(19)=2
30040 DATA"INFORMATION","USER PROFILE","ACCESS/FLAGS","TIME/CREDITS","DOS.","QUIT"
30050 IFPEEK(215)THENPOKE2603,8:PRINTCHR$(27)"e{reverse on}{light gray}                                                                               ":GOTO30120
30060 PRINT"{reverse on}{white} Zelch 128 v2.0      Online Sysop Menu "
30080 WINDOW0,4,PEEK(238),23:IFPEEK(215)THEN30120:ELSEPRINT"{white}{home}{down}":FORX=1TO6:IFC=XTHENPRINT"{cyan}{reverse on}";
30090 PRINTTT$(X)"{white}"
30100 NEXTX:GOTO30150
30120 PRINT"{reverse on}{home}{light gray}";:FORX=1TO6:IFC=XTHENPRINT"{cyan}{right}{cm g}";:ELSEPRINT"{right} ";
30130 PRINTTT$(X);:IFC=XTHENPRINT"{cm m}";:ELSEPRINT" ";
30140 PRINT"{light gray}{right}";:NEXTX
30150 GETKEYA$:IFA$="{right}"ORA$="{down}"THENC=C+1:GOTO30180
30160 IFA$="{left}"ORA$="{up}"THENC=C-1:GOTO30180
30170 IFA$=CHR$(13)THEN30200
30180 IFC>6THENC=1:ELSEIFC<1THENC=6
30190 GOTO30080
30200 IFC=6THEN33200
30300 PRINT:WINDOW0,5,PEEK(238),23,1:ONCGOSUB30910,32000,32500,32700,33000
30310 PRINT:WINDOW0,5,PEEK(238),23:FORX=1TO20:PRINTCHR$(27)"v";:NEXTX:GOTO30080
30400 REM -------- WINDOW ROUTINES --------------------------------------------
30410 WINDOWX-1,Y-1,X+W8,Y+W9,1:PRINTCHR$(27)"m{home}{cyan}";
30420 PRINTLEFT$("{cm @*40}",W8+2)
30430 FORA=1TOW9:PRINT"{cm g}"SPC(W8)"{cm m}":NEXTA
30440 PRINTLEFT$("{cm t*40}",W8+2)
30450 WINDOWX,Y,X+W8-1,Y+W9-1:POKE250,W8-1:POKE2603,0:PRINTCHR$(27)"l"CHR$(27)"f";:RETURN
30460 REM -------- WINDOW ERASE -----------------------------------------------
30470 WINDOWX-1,Y-1,X+W8,Y+W9,1:POKE2603,8:PRINTCHR$(27)"e{cyan}";:RETURN
30500 REM -------- ACCES FLAG TOGGLE ROUTINE ----------------------------------
30510 FORI=1TO26:IFMID$(I$,I,1)="1"THENPRINT"{reverse on}";
30520 PRINTCHR$(I+192)"{reverse off}";:NEXTI:FORI=1TO26:PRINT"{left}";:NEXTI:I=1
30530 GETA$:SYS15628:IFA$=""THEN30530
30540 IFA$=CHR$(13)THENPRINT:RETURN
30550 IFA$="{right}"ORA$="{up}"THENI=I+1:GOTO30590
30560 IFA$="{left}"ORA$="{down}"THENI=I-1:GOTO30600
30570 MID$(I$,I,1)=MID$(STR$(ABS(VAL(MID$(I$,I,1))-1)),2):IFMID$(I$,I,1)="1"THENPRINT"{reverse on}";
30580 PRINTCHR$(I+192)"{reverse off}{left}";:IFA$<>" "THEN30530:ELSEI=I+1
30590 IFI>26THENI=26:GOTO30530:ELSEPRINT"{right}";:GOTO30530
30600 IFI<1THENI=1:GOTO30530:ELSEPRINT"{left}";:GOTO30530
30900 REM -------- INFORMATION ------------------------------------------------
30910 PRINT" {cyan}{reverse on}{cm t*33}"
30920 PRINT" {reverse on} Help and information goeth here "
30930 PRINT" {reverse on}                                 "
31999 GETKEYA$:RETURN
32000 REM -------- USER PROFILE -----------------------------------------------
32010 IFPEEK(215)THENWINDOW16,5,79,23
32020 PRINT"{cyan}{reverse on}{cm t*26}"
32030 PRINT"{reverse on} <A> Edit caller's handle "
32040 PRINT"{reverse on} <B> Change password      "
32050 PRINT"{reverse on} <C> Real name            "
32060 PRINT"{reverse on} <D> Phone number         "
32070 PRINT"{reverse on}                          "
32080 GETKEYA$:W1=INSTR("abcd",CHR$(ASC(A$)AND127)):IFW1=0THENRETURN
32090 X=10:Y=12:W8=20:W9=1:IFPEEK(215)THENX=47:Y=5+W1
32100 GOSUB30400:PRINT"{white}";:O$=U$(W1):SYS4867:GOSUB30470:IFI$=""THEN32080:ELSEFORI=1TOLEN(I$):A=ASC(MID$(I$,I,1)):IFA>64ANDA<91THENA=AOR128
32110 MID$(I$,I,1)=CHR$(A):NEXTI:U$(W1)=I$:GOTO32080
32500 REM -------- ACCESS AND FLAGS -------------------------------------------
32510 IFPEEK(215)THENWINDOW32,5,79,23
32520 PRINT"{cyan}{reverse on}{cm t*25}"
32530 PRINT"{reverse on} <A> Access level        "
32540 PRINT"{reverse on} <B> System access flags "
32545 PRINT"{reverse on} <C> Toggle SysOp flag   "
32550 PRINT"{reverse on}                         "
32560 GETKEYA$:W1=INSTR("abc",CHR$(ASC(A$)AND127)):IFW1=0THENRETURN
32570 X=10:Y=12:W8=19:W9=1:IFPEEK(215)THENX=9:Y=5+W1
32580 ONW1GOTO32590,32650,32670
32590 GOSUB30400:PRINT"New Access Level: {white}";:GETKEYA$:A$=CHR$(ASC(A$)OR128):PRINTA$
32600 IFINSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ",A$)=0THENPRINT"{pink}Invalid level";:SLEEP1:GOTO32620
32610 PRINT"{cyan}Access changed!";:SLEEP1:U$(5)=A$
32620 GOSUB30470:GOTO32560
32650 W8=28:X=5:IFPEEK(215)THENX=1
32660 GOSUB30400:PRINT" ";:I$=U$(7):GOSUB30500:U$(7)=I$:GOTO32620
32670 W1=VAL(MID$(U$(8),4,1)):MID$(U$(8),4,1)=MID$(STR$(ABS(W1-1)),2)
32680 GOSUB30400:IFW1=0THENPRINT"{cyan}SysOp flag ON";:SLEEP1:GOTO32690
32685 PRINT"{cyan}SysOp flag OFF";:SLEEP1
32690 GOSUB30470:GOTO32560
32700 REM -------- TIME AND CREDITS -------------------------------------------
32710 IFPEEK(215)THENWINDOW48,5,79,23
32720 PRINT"{cyan}{reverse on}{cm t*22}"
32730 PRINT"{reverse on} <A> Time remaining   "
32740 PRINT"{reverse on} <B> Calls today      "
32750 PRINT"{reverse on} <C> UD Block credits "
32760 PRINT"{reverse on} <D> System credits   "
32780 PRINT"{reverse on}                      "
32800 GETKEYA$:W1=INSTR("abcd",CHR$(ASC(A$)AND127)):IFW1=0THENRETURN
32810 X=3:Y=12:W8=33:W9=1:IFPEEK(215)THENX=12:Y=5+W1
32820 GOSUB30400:ONW1GOTO32830,32850,32870,32890
32830 PRINT"Time remaining: {white}";:O$=MID$(STR$(W6),2):SYS4867:I=VAL(I$):IFI>0ANDI<256THENW6=I
32840 GOSUB30470:GOTO32800
32850 PRINT"Calls today: {white}";:O$=MID$(STR$(VAL(U$(9))),2):SYS4867:I=VAL(I$):IFI>-1ANDI<1000000THENU$(9)=STR$(I)
32860 GOTO32840
32870 PRINT"UD Blocks: {white}";:O$=MID$(STR$(U(1)),2):SYS4867:I=VAL(I$):IFI>-1ANDI<1000000THENU(1)=I
32880 GOTO32840
32890 PRINT"System credits: {white}";:O$=MID$(STR$(U(2)),2):SYS4867:I=VAL(I$):IFI>-1000000ANDI<1000000THENU(2)=I
32900 GOTO32840
33000 REM -------- DOS COMMANDS -----------------------------------------------
33010 CLOSE1:OPEN1,U,15
33020 X=2:Y=6:W8=35:W9=2:GOSUB30400:PRINT"Device:{white}"STR$(U)"{cyan}, Drive:{white}"STR$(D):INPUT#1,A,A$,B,I:PRINT"{cyan}Status:{yellow}"STR$(A)","A$","STR$(B)","STR$(I);
33030 Y=10:W8=35:W9=1:GOSUB30400:O$=" ":PRINT"@:{white}";:SYS4867:IFI$=""ORI$=" "THENCLOSE1:GOSUB30470:RETURN
33040 A=VAL(LEFT$(I$,2)):IFA=0THEN33050:ELSEU=A:A=INSTR(I$,","):IFA=0THEN33010
33045 D=VAL(MID$(I$,INSTR(I$,",")+1)):GOTO33010
33050 IFLEFT$(I$,1)<>"$"THENPRINT#1,I$:GOTO33020
33060 A=INSTR(I$,":"):IFA=0THENA$="*":ELSEA$=MID$(I$,A+1)
33070 IFLEN(A$)>16THENA$=LEFT$(A$,16)
33080 X=10:Y=13:W8=27:W9=10:IFPEEK(215)THENX=45:Y=6:W9=17
33090 GOSUB30400:CLOSE2:POKE253,0:OPEN2,U,0,"$"+MID$(STR$(D),2)+":"+A$:SYS8219:CLOSE2
33100 PRINT:PRINT"Press RETURN to continue: ";:DO:GETKEYA$:LOOPUNTILA$=CHR$(13):GOSUB30470:GOTO33020
33200 REM -------- EXIT ROUTINE -----------------------------------------------
33210 PRINT:WINDOW0,4,PEEK(238),23,1:POKE2823,W5:POKE2974,W4:POKE2825,W6:POKE2824,1:POKE2827,0
33215 IFW7<>O(19)THENBLOAD(MID$("pxl",W7+1,1)+"-xfer 1300"),U(D(0,0)),D(D(0,1)):O(19)=W7
33220 U=W2:D=W3:POKE2603,0:PRINTCHR$(27)"e"O$(21);:SYS15625:GOSUB100:O$=T$(121):GOSUB4:SYS15634:CLR:SYS15637
33230 DO:GET#5,A$:LOOPUNTILA$="":GOTO920
55555 A=PEEK(186):OPEN1,A,15,"s0:prg.online utils":CLOSE1:DSAVE"prg.online utils",U(A),D0
